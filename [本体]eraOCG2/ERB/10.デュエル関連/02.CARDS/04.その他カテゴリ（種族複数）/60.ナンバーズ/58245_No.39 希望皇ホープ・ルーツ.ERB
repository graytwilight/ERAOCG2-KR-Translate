@CARDNAME_58245,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = No.39 유토피아 루츠
	CASE "略称"
		RESULTS = 유토피아 루츠
	CASE "カテゴリ"
		RESULTS:0 = 넘버즈
		RESULTS:1 = 유토피아
ENDSELECT


@CARD_58245(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 전사족 
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 500
	CASE "守備力"
		RETURN 100
	CASE "추가日"
		RETURN 250919
ENDSELECT

RETURN 0

@CARDSUMMON_58245(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 1
;추가효과用

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,58245) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_58245(종류)
#DIM DYNAMIC 종류

PRINTL レベル１モンスター×２
CALLFORM CARD_ABLE(1,EX덱起動)
PRINTL 「No.39 유토피아 루츠」は１ターンに１度、손패の「ＲＵＭ」마법カード１枚を捨て、
PRINTL 자신フィールドの「유토피아」モンスター１体の上に重ねてＸ소환できる。
CALLFORM CARD_ABLE(0,攻撃時誘発)
PRINTL ①：자신・상대モンスターの攻撃宣言時、このカードのＸ素材を１つ取り除いて발동できる。
PRINTL その攻撃を無効にする。
PRINTL その後、このカードの攻撃力はお互いの戦闘モンスターのランクの合計×５００アップする。



@CARDCAN_58245(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,58245)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF 발동済_1(듀얼리스트,58245)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,58245)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = RUM
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 유토피아
	효과条件重ねてＸ = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_58245(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58245,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件カテゴリ = RUM
		효과条件종류 = 마법全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL RESETCONDITION_MAT
			必要素材数 = 1
			효과条件カテゴリ = 유토피아
			重ねてＸ = 1
			TRYCALLFORM XYZSUMMON(듀얼리스트,場所)
		ENDIF
	ENDIF
	
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58245,0,X消費1)
	IF RESULT == 0
		TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:戦闘場所(듀얼리스트)")},"종류"
		SIF RESULT == 엑시즈
			攻撃力修正 += GETVAR(@"%듀얼리스트%_MZ_星:戦闘場所(듀얼리스트)")

		TRYCALLFORM CARD_{GETVAR(@"%対面者%_필드:戦闘場所(듀얼리스트,1)")},"종류"
		SIF RESULT == 엑시즈
			攻撃力修正 += GETVAR(@"%対面者%_MZ_星:戦闘場所(듀얼리스트,1)")

		攻撃力修正 = 攻撃力修正 * 500
		CALL EVADE_ATTACK(듀얼리스트)

		CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"永攻",攻撃力修正)
	ENDIF
ENDIF