@CARDNAME_72754,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 대파괴수 접근경보
	CASE "略称"
		RESULTS = 대파괴수 접근경보
	CASE "カテゴリ"
		RESULTS:0 = 대파괴수
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_72754(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "レア度"
		RETURN RARE
	CASE "추가日"
		RETURN 250923
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72754(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：자신 메인 페이즈에 발동할 수 있다。
PRINTL 덱 / 묘지에서 "대파괴수" 몬스터 １장을 패에 넣는다。
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：필드에 "파괴수" 몬스터가 존재하는 경우, 묘지의 이 카드를 제외하고 발동할 수 있다。
PRINTL 덱 / 묘지에서 "대파괴수 연구소" １장을 패에 넣는다。

@CARDCAN_72754(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72754)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,72754)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 대파괴수
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,72754)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	효과条件カテゴリ = 파괴수
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件名前 = 대파괴수 연구소
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_72754(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72754,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 대파괴수
		효과条件종류 = モン全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72754,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件名前 = 대파괴수 연구소
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF