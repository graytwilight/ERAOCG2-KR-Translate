@CARDNAME_25238,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마요선수 독안군주
	CASE "略称"
		RESULTS = 독안군주
	CASE "カテゴリ"
		RESULTS:0 = 요선수
ENDSELECT

@CARD_25238(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 3000
	CASE "分割"
		RETURN 펜듈럼
	CASE "추가日"
		RETURN 250915
ENDSELECT
RETURN 0

@CARD_EXPLANATION_25238(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("P_MAGIC")
PRINTL 이 카드명의 ①의 Ｐ효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,마함起動)
PRINTL ①：자신의 마법＆함정 존에 다른 Ｐ몬스터가 존재하는 경우에 발동할 수 있다。
PRINTL 패에서 야수족 몬스터를 임의의 수 만큼 특수 소환한다。
CALLFORM CARD_ABLE(0,ターン終了時強制)
PRINTL ②：자신 엔드 페이즈에 발동한다。
PRINTL 이 카드를 원래 주인의 패로 되돌린다。
CALL TEXT_DECORATION("P_MAGIC_2")
CALLFORM CARD_ABLE(0,소환時,특수소환時)
PRINTL ①：이 카드가 소환 / 특수 소환에 성공한 경우、상대 필드의 카드 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 카드를 원래 주인의 패로 되돌린다。
CALLFORM CARD_ABLE(0,효과後強制誘発)
PRINTL ②：이 카드가 몬스터 존에 존재하고、이 카드 이외의 필드의 카드가 패 / 덱에 되돌아갈 때 마다 발동한다。
PRINTL 자신 필드의 모든 "요선수" 몬스터의 공격력은 ５００오른다。
CALLFORM CARD_ABLE(0,ターン終了時強制)
PRINTL ③：이 카드를 특수 소환한 턴의 엔드 페이즈에 발동한다。
PRINTL 이 카드를 원래 주인의 패로 되돌린다。

@CARDCAN_25238(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,25238)
SIF RESULT == 1
	RETURN 0

;P
;설치
IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	;상대使用時
	;ひたすらPゾーンに置きまくるのが気になったので
	IF 듀얼리스트 == "상대"
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件名前 = 마요선수 독안군주
		효과条件표시 = 表側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			RETURN 0	
	ENDIF
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF 발동済_1(듀얼리스트,25238)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件分割 = 펜듈럼
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종족 = 야수족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;M
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0	
	RETURN 1
ELSEIF 종류 == 효과後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"손패덱")
	IF RESULT != 1
		CALL FULLRESET
		CALL CARD_CHECK_FIN_DATA(対面者,"손패덱")
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == ターン終了時強制
	IF ゾーン != "마함"
		SIF ゾーン != "필드"
			RETURN 0
		SIF ゾーン == "필드" && GETVAR(@"%듀얼리스트%_MZ_T1召種:場所") == 召種通常
			RETURN 0
	ENDIF
	RETURN 1
ENDIF


@CARDEFFECT_25238(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 最大소환数
#DIM DYNAMIC 추가효과

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

;P
;설치
IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25238)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25238,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종족 = 야수족
		효과条件소환可 = 1
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		最大소환数 = RESULT 
		
		SIF 最大소환数 > 5 - モンスター数(듀얼리스트)
			最大소환数 = 5 - モンスター数(듀얼리스트)

		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종족 = 야수족
		효과条件複数 = 100 - 最大소환数
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
;M
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25238,0)
	IF RESULT == 0
		;대인화시のモン효과②
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件名前 = 마요선수 대인화시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			추가효과 = 1
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		IF 추가효과 == 0
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"손패")
		ELSEIF 추가효과 == 1
			PRINTL 독안군주의 효과로 덱으로 되돌아갑니다。
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"덱")
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後強制誘発
	SETCOLOR C_YELLOW
	PRINTFORML 독안군주의 효과발동！
	PRINTFORML 요선수 몬스터의 공격력이 ５００올랐다！
	RESETCOLOR
	CALL FULLRESET
	효과条件カテゴリ = 요선수
	효과条件종류 = モン全般
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"永攻",500)
ELSEIF 종류 == ターン終了時強制
	IF ゾーン == "필드"
		CALL CARD_DISPOSE(듀얼리스트,"필드",場所,3)
	ELSEIF ゾーン == "마함"
		CALL CARD_DISPOSE(듀얼리스트,"마함",場所,3)
	ENDIF
ENDIF