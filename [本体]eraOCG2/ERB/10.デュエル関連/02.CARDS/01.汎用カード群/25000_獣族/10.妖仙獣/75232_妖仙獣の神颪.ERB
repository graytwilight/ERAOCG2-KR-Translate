@CARDNAME_75232,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 요선수의 신풍
	CASE "略称"
		RESULTS = 요선수의 신풍
	CASE "カテゴリ"
		RESULTS:0 = 요선수
ENDSELECT


@CARD_75232(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "추가日"
		RETURN 250915
ENDSELECT

RETURN 0

@CARD_EXPLANATION_75232(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動 ,손패起動)
PRINTL ①：이하의 효과에서 １개를 선택하고 발동할 수 있다（자신 필드에 몬스터가 존재하지 않는 경우、양쪽을 선택할 수 있다）。
PRINTL ●덱에서 "요선수" 몬스터 １장을 패에 넣는다。
PRINTL 그 후、레벨４ 이하의 "요선수" 몬스터 １장의 소환을 할 수 있다。
PRINTL ●덱에서 카드명이 다른 "요선수" Ｐ몬스터 ２장을 골라、자신의 마법＆함정 존에 앞면 표시로 둔다。
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：묘지의 이 카드를 제외하고、자신 필드의 앞면 표시인 "요선수" 마법 / 함정 카드 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 카드를 원래 주인의 패로 되돌린다。

@CARDCAN_75232(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,75232)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,75232)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 요선수
	효과条件分割 = 펜듈럼
	효과条件CNT = 1
	효과条件固名前起動 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT < 2 || 마함数(듀얼리스트) > 4
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 요선수
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,75232)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件カテゴリ = 요선수
	효과条件표시 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_75232(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 발동불가,2
#DIM DYNAMIC 選択효과
#DIMS DYNAMIC 選択カード名前

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,75232,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;『발동불가:0』は①の下の효과、『발동불가:1』は上下両方の효과を適用できるか판정する変数です
		;덱に「요선수」Ｐモンスターが２종류いなければ下の효과は使えない
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 요선수
		효과条件分割 = 펜듈럼
		효과条件CNT = 1
		효과条件固名前起動 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT < 2
			발동불가:0 = 1
		;마법＆함정ゾーンに空きが無ければ下の효과は使えない
		SIF 마함数(듀얼리스트) > 3
			발동불가:0 = 1
		;以上の条件を満たした上で、마법＆함정ゾーンに置く用とサーチする用で３枚の요선수モンスターが덱になければ両方の효과を適用できない
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 요선수
		효과条件종류 = モン全般
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT < 3 || 발동불가:0 == 1 || モンスター数(듀얼리스트) != 0
			발동불가:1 = 1
		IF 듀얼리스트 == "자신"
			PRINTFORML 사용하는 효과를 선택해주세요。
			PRINTL [0] ●덱에서 "요선수" 몬스터 １장을 패에 넣는다。
			PRINTL [0]  그 후、레벨４ 이하인 "요선수" 몬스터 １장을 소환할 수 있다。
			SIF 발동불가:0 == 0
				PRINTL [1] ●덱에서 카드명이 다른 "요선수" Ｐ몬스터 ２장을 골라、자신의 마법＆함정 존에 앞면 표시로 놓는다。
			SIF 발동불가:1 == 0
				PRINTL [2] ●둘 다의 효과
			BINPUT
			選択효과 = RESULT
		;상대使用時
		;손패or場に2종류のPモンが揃っていなければ置く효과を使う
		ELSEIF 듀얼리스트 == "상대" && (발동불가:0 == 0 || 발동불가:1 == 0)
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲마함 = 1
			효과条件カテゴリ = 요선수
			효과条件分割 = 펜듈럼
			효과条件CNT = 1
			효과条件固名前起動 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT < 2 && 발동불가:1 == 0
				選択효과 = 2
			ELSEIF RESULT < 2 && 발동불가:0 == 0
				選択효과 = 1
			ENDIF
		ENDIF

		IF 選択효과 == 0
			CALL CARDEFFECT_75232_1(듀얼리스트,종류,ゾーン,場所)
		ELSEIF 選択효과 == 1
			CALL CARDEFFECT_75232_2(듀얼리스트,ゾーン,場所)
		ELSEIF 選択효과 == 2
			PRINTL 마법＆함정 존에 놓을 카드를 선택해 주세요。
			CALL CARDEFFECT_75232_2(듀얼리스트,ゾーン,場所)
			PRINTL 패에 넣을 카드를 골라주세요。
			CALL CARDEFFECT_75232_1(듀얼리스트,종류,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,75232,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件カテゴリ = 요선수
		효과条件표시 = 1
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

@CARDEFFECT_75232_1(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 종류
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID

CALL FULLRESET
효과範囲덱 = 1
효과条件カテゴリ = 요선수
효과条件종류 = モン全般
CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
IF RESULT == 1
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 요선수
	CALL 효과条件レベル_ADV(4,"以下")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		IF 듀얼리스트 == "자신"
			PRINTL 몬스터의 소환을 하시겠습니까？
			PRINTL [0] 예
			PRINTL [1] 아니오
			BINPUT
		ENDIF
		IF RESULT == 0 || (듀얼리스트 == "상대" && モンスター数(듀얼리스트) < 5)
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件カテゴリ = 요선수
			효과条件종류 = モン全般
			CALL 효과条件レベル_ADV(4,"以下")
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			選択カード = RESULT
			選択カードID = RESULT:99
			IF 選択カードID != 9999
				CALL CARD_SELECT(듀얼리스트,選択カード)
				選択カード = RESULT
				CALL CARD_SUMMON_SHORI(듀얼리스트,選択カード,"攻撃표시",3)
			ENDIF
		ENDIF
	ENDIF
ENDIF

@CARDEFFECT_75232_2(듀얼리스트,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名前

CALL FULLRESET
효과範囲덱 = 1
효과条件カテゴリ = 요선수
효과条件分割 = 펜듈럼
CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
選択カードID = RESULT:1
TRYCALLFORM CARDNAME_{選択カードID},"名前"
選択カード名前= %RESULTS%
CALL FULLRESET
효과範囲덱 = 1
효과条件カテゴリ = 요선수
효과条件分割 = 펜듈럼
효과条件固名前 = %選択カード名前%
CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")