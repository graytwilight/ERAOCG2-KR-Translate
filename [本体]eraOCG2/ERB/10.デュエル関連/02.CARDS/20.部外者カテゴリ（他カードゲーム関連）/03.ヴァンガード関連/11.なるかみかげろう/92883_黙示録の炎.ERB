@CARDNAME_92883,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 묵시록의 화염
	CASE "略称"
		RESULTS = 묵시록의 화염
	CASE "カテゴリ"
		RESULTS:0 = 카게로우
		RESULTS:1 = 오버로드
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_92883(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 지속함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92883(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드 이름의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①：以下の효과から１つを選択して발동できる。
PRINTL ●자신の손패・묘지から「오버로드」モンスター１体を특수소환する。
PRINTL ●덱から「묵시록의 화염」以外の「오버로드」カード１枚を손패に加える。
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②：자신フィールドの「오버로드」モンスターは대상耐性を得る。
CALLFORM CARD_ABLE(2,攻撃後誘発)
PRINTL ③：자신の「오버로드」モンスターが戦闘で상대モンスターを破壊した場合に발동できる。
PRINTL このターン、そのモンスターはもう１度攻撃できる。


@CARDCAN_92883(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 효과使用불가,2

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92883)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_1(듀얼리스트,92883)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 오버로드
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과使用불가:0 = 1
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 오버로드
	효과条件固名前 = 묵시록의 화염
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과使用불가:1 = 1
	SIF 효과使用불가:0 == 1 && 효과使用불가:1 == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3	
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後誘発
	SIF 발동済_2(듀얼리스트,92883)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	SIF 戦闘場所(듀얼리스트) == 5
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)},"カテゴリ"
	SIF !MATCH(RESULTS,"오버로드")
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_92883(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC ダメージ量
#DIM DYNAMIC 효과使用불가,2


IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92883,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 오버로드
		효과条件종류 = モン全般
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			효과使用불가:0 = 1
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 오버로드
		효과条件固名前 = 묵시록의 화염
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			효과使用불가:1 = 1

		IF 듀얼리스트 == "자신"
			IF 효과使用불가:0 != 1 && 효과使用불가:1 != 1
				CALL YN_CHOICE(듀얼리스트,"●패 / 묘지에서「오버로드」몬스터 １장을 특수 소환한다。","●덱에서「오버로드」카드 １장을 패에 넣는다。")
			ELSEIF 효과使用불가:0 != 1
				RESULT = 0
			ELSEIF 효과使用불가:1 != 1
				RESULT = 1
			ELSE
				PRINTW 발동 가능한 효과가 없습니다
				RETURN 0
			ENDIF
		ELSE
			IF 효과使用불가:0 != 1
				RESULT = 0
			ELSEIF 효과使用불가:1 != 1
				RESULT = 1
			ELSE
				RETURN 0
			ENDIF
		ENDIF

		IF RESULT == 0
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲묘지 = 1
			효과条件カテゴリ = 오버로드
			효과条件종류 = モン全般
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)	
		ELSEIF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 오버로드
			효과条件固名前 = 묵시록의 화염
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92883)
	IF RESULT == 0
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件カテゴリ = 오버로드
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"対耐")
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92883,2)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",戦闘場所(듀얼리스트),"戦",1)
	ENDIF
ENDIF