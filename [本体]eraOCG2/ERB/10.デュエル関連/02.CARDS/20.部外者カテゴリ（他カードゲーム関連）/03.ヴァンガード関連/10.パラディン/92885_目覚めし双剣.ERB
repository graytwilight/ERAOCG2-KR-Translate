@CARDNAME_92885,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 눈을 뜬 쌍검
	CASE "略称"
		RESULTS = 눈을 뜬 쌍검
	CASE "カテゴリ"
		RESULTS:0 = 블래스터 블레이드
		RESULTS:1 = 블래스터 다크
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_92885(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92885(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
PRINTL 자신 필드에 "블래스터 블레이드" 또는 "블래스터 다크"가 존재하는 경우、이 카드는 세트한 턴에도 발동할 수 있다。
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：자신의 덱 / 묘지 / 제외상태인 "블래스터 블레이드" 또는 "블래스터 다크" １체를 특수소환한다。
PRINTL 그 후、이하의 효과를 적용할 수 있다。
PRINTL ●자신의 패 / 필드의 몬스터를 융합소재로 하고、융합 몬스터 １장을 융합 소환한다。
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：이 카드가 묘지에 존재하는 경우、자신 메인 페이즈에 자신 묘지의 융합 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터를 EX덱으로 되돌린다。그 후、이 카드를 자신 필드에 세트한다。
PRINTL 이 효과로 세트한 이 카드는 필드에서 벗어날 경우에 제외된다。


@CARDCAN_92885(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92885)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,92885)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件名前:0 = 블래스터 블레이드
	효과条件名前:1 = 블래스터 다크
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件名前:0 = 블래스터 블레이드
	효과条件名前:1 = 블래스터 다크
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3 && RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,92885)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_92885(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92885,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件名前:0 = 블래스터 블레이드
		효과条件名前:1 = 블래스터 다크
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL RESETAREA_MAT
			素材範囲필드 = 1
			素材範囲손패 = 1
			CALL FULLRESET
			효과範囲EX덱 = 1
			효과条件종류 = 융합
			효과条件소환可 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				CALL RESETAREA_MAT
				素材範囲필드 = 1
				素材範囲손패 = 1
				CALL FULLRESET
				효과範囲EX덱 = 1
				효과条件종류 = 융합
				효과条件소환可 = 1
				효과条件任意 = 1
				CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
				選択カード = RESULT
				IF 選択カード != 9999
					TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
					CALL FUSSUMMON(듀얼리스트,選択カード)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92885,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종류 = 융합
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과추가종류 = 500
			효과条件名前 = 눈을 뜬 쌍검
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
		ENDIF
	ENDIF
ENDIF
