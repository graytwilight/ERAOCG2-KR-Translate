@CARD_PACK_CHOOSE(설정종류)
#DIM DYNAMIC 初回カウンタ
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択덱
#DIM DYNAMIC カウンタB
#DIM DYNAMIC スタート数
#DIM DYNAMIC エンド数

#DIM DYNAMIC パックセット,1000
#DIM DYNAMIC 설정종류

$PACK_LOOP
CUSTOMDRAWLINE ―
PRINTFORML 　팩을 골라주세요
CUSTOMDRAWLINE ―
VARSET カウンタB

SELECTCASE 설정종류
CASE 3
	スタート数 = 1
	エンド数 = 100
CASE 2
	スタート数 = 90
	エンド数 = 100
CASE 1
	スタート数 = 1
	エンド数 = 80
ENDSELECT

FOR カウンタ,スタート数,エンド数
	VARSET RESULT
	VARSET RESULTS
	CALL CARD_PACK_NAME(カウンタ)
	SELECTCASE 설정종류
	CASE 3
		IF RESULTS != ""
			パックセット:カウンタB = カウンタ
			カウンタB ++
		ENDIF
	CASEELSE
		IF RESULT == 1
			パックセット:カウンタB = カウンタ
			カウンタB ++
		ENDIF
	ENDSELECT
NEXT

FOR カウンタ,0,カウンタB
	VARSET RESULTS
	CALL CARD_PACK_NAME(パックセット:カウンタ)
	IF RESULTS != ""
		PRINTFORM 　　[{カウンタ,3}] %RESULTS,50,LEFT%　
		SETCOLOR C_P_GREEN
		PRINTBUTTON "[수록확인]",パックセット:カウンタ + 5000
		RESETCOLOR
	ENDIF
	SIF カウンタ == カウンタB - 1
		PRINTL
NEXT
PRINTL
CUSTOMDRAWLINE ―
PRINTL [9999] - 돌아간다
BINPUT
SIF RESULT == 9999
	RETURN 0
IF RESULT == 9990
	スタート数 -= 10
	SIF スタート数 <= 0
		スタート数 = 0
	GOTO PACK_LOOP
ELSEIF RESULT == 9991
	スタート数 += 10
	SIF スタート数 >= 100
		スタート数 = 0
	GOTO PACK_LOOP
ENDIF
IF INRANGE(RESULT,0,1000)
	CALL CARD_PACK(パックセット:RESULT)
	GOTO PACK_LOOP
ELSEIF INRANGE(RESULT,5000,6000)
	CALL SP_PACKPOOL_LOAD(RESULT - 5000)
	$LIST_LOOP
	VARSET カウンタB
	SETCOLOR C_ROYAL_BLUE
	PRINTFORML ―【울트라 레어】―――――――――――――――――――――――――――――――――――――――――――
	FOR カウンタ,0,URプール最大値()
		CALLFORM CARDNAME_{URプール:カウンタ}("略称")
		PRINT 　
		PRINTBUTTON @"[ULR]-%RESULTS,16,LEFT%",URプール:カウンタ
		PRINT 　
		カウンタB ++
		IF カウンタB >= 5
			PRINTL
			VARSET カウンタB
		ENDIF
	NEXT
	PRINTL
	VARSET カウンタB
	SETCOLOR C_GOLD
	PRINTFORML ―【슈퍼 레어】―――――――――――――――――――――――――――――――――――――――――――
	FOR カウンタ,0,SRプール最大値()
		CALLFORM CARDNAME_{SRプール:カウンタ}("略称")
		PRINT 　
		PRINTBUTTON @"[SPR]-%RESULTS,16,LEFT%",SRプール:カウンタ
		PRINT 　
		カウンタB ++
		IF カウンタB >= 5
			PRINTL
			VARSET カウンタB
		ENDIF
	NEXT
	IF パック条件CMN != 1
		PRINTL
		VARSET カウンタB
		SETCOLOR C_SILVER
		PRINTFORML ―【레어】―――――――――――――――――――――――――――――――――――――――――――――――
		FOR カウンタ,0,Rプール最大値()
			CALLFORM CARDNAME_{Rプール:カウンタ}("略称")
			PRINT 　
			PRINTBUTTON @"[ R ]-%RESULTS,16,LEFT%",Rプール:カウンタ
			PRINT 　
			カウンタB ++
			IF カウンタB >= 5
				PRINTL
				VARSET カウンタB
			ENDIF
		NEXT
		PRINTL
		VARSET カウンタB
		RESETCOLOR
		PRINTFORML ―【커먼】――――――――――――――――――――――――――――――――――――――――――――――

		FOR カウンタ,0,CMプール最大値()
			CALLFORM CARDNAME_{CMプール:カウンタ}("略称")
			PRINT 　
			PRINTBUTTON @"[CMN]-%RESULTS,16,LEFT%", CMプール:カウンタ
			PRINT 　
			カウンタB ++
			IF カウンタB >= 5
				PRINTL
				VARSET カウンタB
			ENDIF
		NEXT
	ELSE
		RESETCOLOR
		PRINTL
		PRINTFORML ―【레어 커먼 묶음】――――――――――――――――――――――――――――――――――――――
		PRINTFORML 
		PRINTFORML 
	ENDIF
	RESETCOLOR

	PRINTL
	CUSTOMDRAWLINE ―
	PRINTBUTTON "[9999] - 돌아간다", 99999
	BINPUT
	IF RESULT != 99999
;		PRINTL ■ 카드정보
		選択カード_ID = RESULT
		TRYCALLFORM CARD_UI(選択カード_ID,1);　パックリストでのカード情報確認
		GOTO LIST_LOOP
	ENDIF
	GOTO PACK_LOOP
ENDIF



@CARD_DECK_CHECK(選択덱,설정先,仮組確認)
#DIM DYNAMIC 選択덱
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 설정先
#DIM DYNAMIC 仮組確認

VARSET RESULT
SETCOLOR C_YELLOW
CUSTOMDRAWLINE ―
IF 選択덱 >= 1000
	VARSET RESULTS
	TRYCALLFORM DUELDECK_{選択덱}("存在確認")
	PRINTFORM 【%RESULTS%】 
	IF RESULTS:1 != ""
		PRINTFORM -%RESULTS:1%-
	ENDIF
ELSE
	IF 마이덱名:選択덱 != ""
		PRINTFORM 【%마이덱名:選択덱%】 
	ELSE
		PRINTFORM 【마이덱:{選択덱}】 
	ENDIF
ENDIF
PRINTFORML 
CUSTOMDRAWLINE ―
RESETCOLOR
IF 仮組確認 == 0
	VARSET 仮組_덱, -1
	VARSET 仮組_EX덱, -1
	IF 選択덱 >= 1000
		CALLFORM DUELDECK_{選択덱}("仮組")
	ELSE
		CALLFORM MYDECK("仮組",選択덱)
	ENDIF
ENDIF
$RETURN_DECK
VARSET カウンタB
CUSTOMDRAWLINE ―
PRINTFORML ■ 메인덱
CUSTOMDRAWLINE ―
FOR カウンタ,0,40
	SIF 仮組_덱:カウンタ == -1
		CONTINUE
	CALL CARD_SETCOLOR("仮組","덱",カウンタ)
	VARSET RESULTS
	TRYCALLFORM CARDNAME_{仮組_덱:カウンタ}, "名前"
	STRLENS RESULTS
	IF RESULT >= 40
		SUBSTRING RESULTS, 0, 37
		PRINTBUTTON @"　[{カウンタ + 101,3}]　%RESULTS,38,LEFT%…", カウンタ + 101
	ELSE
		SUBSTRING RESULTS, 0, 40
		PRINTBUTTON @"　[{カウンタ + 101,3}]　%RESULTS,40,LEFT%", カウンタ + 101
	ENDIF

	RESETCOLOR
	カウンタB ++
	SIF カウンタB % 3 == 0
		PRINTFORML
NEXT
SIF カウンタB % 3 != 0
	PRINTL
VARSET カウンタB
CUSTOMDRAWLINE ―
PRINTFORML ■ 엑스트라덱
CUSTOMDRAWLINE ―
FOR カウンタ,0,15
	SIF 仮組_EX덱:カウンタ == -1
		CONTINUE
	CALL CARD_SETCOLOR("仮組","EX덱",カウンタ)
	VARSET RESULTS
	TRYCALLFORM CARDNAME_{仮組_EX덱:カウンタ}, "名前"
	STRLENS RESULTS
	IF RESULT >= 40
		SUBSTRING RESULTS, 0, 37
		PRINTBUTTON @"　[{カウンタ + 151,3}]　%RESULTS,38,LEFT%…", カウンタ + 151
	ELSE
		SUBSTRING RESULTS, 0, 40
		PRINTBUTTON @"　[{カウンタ + 151,3}]　%RESULTS,40,LEFT%", カウンタ + 151
	ENDIF

	RESETCOLOR
	カウンタB ++
	SIF カウンタB % 3 == 0
		PRINTFORML
NEXT
SIF カウンタB % 3 != 0
	PRINTL


TRYCALLFORM DUELDECK_KAI_{選択덱}
CUSTOMDRAWLINE ―

VARSET RESULT
TRYCALLFORM DUELDECK_MOVE_{選択덱}("存在確認")
IF RESULT == 1
	PRINTBUTTON @"[루트 해설을 읽는다]",8888
	PRINTL
	CUSTOMDRAWLINE ―
ENDIF

PRINTL


IF 설정先 == 1
	IF CFLAG:MASTER:현재위치 == 場所_裏通り
		PRINTL 이 덱을 구입하시겠습니까？（25000엔）
	ELSE
		PRINTL 이 덱을 구입하시겠습니까？（2500엔）
	ENDIF
	PRINTL [0] - 예　　　　[1] - 아니오
ELSEIF 설정先 == 2
	PRINTL 이 덱을 읽어오시겠습니까？
	PRINTL [0] - 예　　　　[1] - 아니오
ELSEIF 설정先 == 3
	PRINTL 이 덱으로 게임을 시작하시겠습니가？
	PRINTL [0] - 예　　　　[1] - 아니오
ELSEIF 설정先 == 4
	PRINTL [1000] - 확인완료
ENDIF
BINPUT
IF INRANGE(RESULT,100,140)
	RESULT -= 101
	選択カード_ID = 仮組_덱:RESULT
	TRYCALLFORM CARD_UI(選択カード_ID,1);　덱リストでのカード情報確認
	GOTO RETURN_DECK
ELSEIF INRANGE(RESULT,150,170)
	RESULT -= 151
	選択カード_ID = 仮組_EX덱:RESULT
	TRYCALLFORM CARD_UI(選択カード_ID,1);　덱リストでのカード情報確認
	GOTO RETURN_DECK
ELSEIF RESULT == 8888
	TRYCALLFORM DUELDECK_MOVE_{選択덱}("ルート解説")
	PRINTW
	GOTO RETURN_DECK
ELSE
	RETURN RESULT
ENDIF
