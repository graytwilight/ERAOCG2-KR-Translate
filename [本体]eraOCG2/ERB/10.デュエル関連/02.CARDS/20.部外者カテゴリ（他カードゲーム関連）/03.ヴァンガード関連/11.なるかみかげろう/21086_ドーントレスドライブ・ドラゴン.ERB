@CARDNAME_21086,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 돈트리스드라이브 드래곤
	CASE "略称"
		RESULTS = 돈트리
	CASE "カテゴリ"
		RESULTS:0 = 카게로우
		RESULTS:1 = 돈트리스
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_21086(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2500
	CASE "성별"
		RETURN 無性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21086(종류)
#DIM DYNAMIC 종류


PRINTL 이 카드명의 ①②③의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：상대 필드의 몬스터의 수가 자신 필드의 몬스터보다 많은 경우에 발동할 수 있다。
PRINTL 이 카드를 패에서 특수 소환한다。
CALLFORM CARD_ABLE(2,필드→묘지 ,손패→묘지 ,덱→묘지 ,破壊→묘지 ,戦闘→묘지)
PRINTL ②：이 카드가 묘지로 보내진 경우、자신 필드의 앞면 표시인 "카게로우" 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터의 공격력은 턴 종료시까지 １０００오른다。
CALLFORM CARD_ABLE(3,攻撃後誘発)
PRINTL ③：자신의 "카게로우" 몬스터가 공격을 행한 데미지 스텝 종료시에、
PRINTL 묘지의 이 카드를 제외하고、패를 １장 버리고 발동할 수 있다。
PRINTL 이 턴、그 몬스터는 １번 더 공격할 수 있다。

@CARDCAN_21086(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 자신モン数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21086)
SIF RESULT == 1
	RETURN 0

IF 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,21086)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,21086,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1	
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	자신モン数 = RESULT
	CALL FULLRESET
	효과範囲상대필드 = 1	
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT <= 자신モン数
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지 || 종류 == 戦闘→묘지
	SIF 발동済_2(듀얼리스트,21086)
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件표시 = 表側표시
	효과条件カテゴリ = 카게로우
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後誘発
	SIF 발동済_3(듀얼리스트,21086)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 손패数(듀얼리스트) < 1
		RETURN 0
	SIF 戦闘場所(듀얼리스트) == 5
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)},"カテゴリ"
	SIF !MATCH(RESULTS,"카게로우")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21086(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード


IF 종류 == 묘지起動
CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21086,1)
	IF RESULT == 0
	CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
	ENDIF
ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지 || 종류 == 戦闘→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21086,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件표시 = 表側표시
		효과条件カテゴリ = 카게로우
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一攻",1000)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21086,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL SET_FIELD_INFO(듀얼리스트,"필드",戦闘場所(듀얼리스트),"戦",1)
		ENDIF
	ENDIF
ENDIF