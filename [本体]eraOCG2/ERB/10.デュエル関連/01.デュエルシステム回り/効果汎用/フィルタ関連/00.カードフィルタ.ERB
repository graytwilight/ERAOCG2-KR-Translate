@CARD_FILTER_BASIC(듀얼리스트,元参照先,ゾーン,場所,めくり数,チェック플래그,カードID)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 実行者
#DIM DYNAMIC めくり数

#DIMS DYNAMIC 元参照先
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC 上級플래그
#DIM DYNAMIC 上級値
#DIM DYNAMIC 参照先セットアップ, 30

#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC カウンタC
#DIM DYNAMIC カウンタD
#DIM DYNAMIC チェック플래그
#DIM DYNAMIC カードID
#DIM DYNAMIC サーチ範囲
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성

#DIM DYNAMIC 条件合致数
#DIM DYNAMIC 条件合致レベル数
#DIM DYNAMIC 条件合致攻撃力数
#DIM DYNAMIC 条件合致守備力数
#DIM DYNAMIC 条件合致魔力Ｃ数
#DIM DYNAMIC 条件合致Ｘ素材数
#DIM DYNAMIC 条件合致個別Ｃ数
#DIM DYNAMIC 条件合致悪性Ｃ数
#DIM DYNAMIC 条件合致薬毒Ｃ数

#DIM DYNAMIC 現在最大攻撃力
#DIM DYNAMIC 現在最小攻撃力
#DIMS DYNAMIC MZ魔판정
#DIM DYNAMIC スーパー条件달성
#DIM DYNAMIC 대상カードID
#DIM DYNAMIC 裏側플래그
#DIM DYNAMIC 攻撃力설정

#DIM DYNAMIC 성별판정

;PRINTFORML 듀얼리스트 = %듀얼리스트%
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

VARSET 選択カード, -1
VARSET 現在最大攻撃力, -1
VARSET 現在最小攻撃力, 999999


IF 元参照先 == "上級"
	SIF 효과範囲필드 == 1
		参照先セットアップ:0 = 1
	SIF 효과範囲마함 == 1
		参照先セットアップ:1 = 1
	SIF 효과範囲손패 == 1
		参照先セットアップ:2 = 1
	SIF 효과範囲묘지 == 1
		参照先セットアップ:3 = 1
	SIF 효과範囲덱 == 1
		参照先セットアップ:4 = 1
	SIF 효과範囲EX덱 == 1
		参照先セットアップ:5 = 1
	SIF 효과範囲제외 == 1
		参照先セットアップ:6 = 1

	SIF 효과範囲상대필드 == 1
		参照先セットアップ:10 = 1
	SIF 효과範囲상대마함 == 1
		参照先セットアップ:11 = 1
	SIF 효과範囲상대손패 == 1
		参照先セットアップ:12 = 1
	SIF 효과範囲상대묘지 == 1
		参照先セットアップ:13 = 1
	SIF 효과範囲상대덱 == 1
		参照先セットアップ:14 = 1
	SIF 효과範囲상대EX덱 == 1
		参照先セットアップ:15 = 1
	SIF 효과範囲상대제외 == 1
		参照先セットアップ:16 = 1
	上級플래그 = 1
ENDIF


$INPUTLOOP
IF 上級플래그
	IF 参照先セットアップ:カウンタB == 0
		カウンタB ++
		IF カウンタB <= 19
			GOTO INPUTLOOP
		ELSE
			GOTO INPUTSKIP
		ENDIF
	ENDIF
	IF カウンタB == 0
		元参照先 = 필드
	ELSEIF カウンタB == 1
		元参照先 = 마함
	ELSEIF カウンタB == 2
		元参照先 = 손패
	ELSEIF カウンタB == 3
		元参照先 = 묘지
	ELSEIF カウンタB == 4
		元参照先 = 덱
	ELSEIF カウンタB == 5
		元参照先 = EX덱
	ELSEIF カウンタB == 6
		元参照先 = 제외

	ELSEIF カウンタB == 10
		元参照先 = 상대필드
	ELSEIF カウンタB == 11
		元参照先 = 상대마함
	ELSEIF カウンタB == 12
		元参照先 = 상대손패
	ELSEIF カウンタB == 13
		元参照先 = 상대묘지
	ELSEIF カウンタB == 14
		元参照先 = 상대덱
	ELSEIF カウンタB == 15
		元参照先 = 상대EX덱
	ELSEIF カウンタB == 16
		元参照先 = 상대제외
	ENDIF
ENDIF

IF 元参照先 == "필드"
	サーチ範囲 = 5
	実行者 = %듀얼리스트%
	参照先 = 필드
ELSEIF 元参照先 == "마함"
	サーチ範囲 = 5
	実行者 = %듀얼리스트%
	参照先 = 마함
	上級値 = 10
ELSEIF 元参照先 == "손패"
	サーチ範囲 = 紙束最大値
	実行者 = %듀얼리스트%
	参照先 = 손패
	上級値 = 100
ELSEIF 元参照先 == "묘지"
	サーチ範囲 = 紙束最大値
	実行者 = %듀얼리스트%
	参照先 = 묘지
	上級値 = 200
ELSEIF 元参照先 == "덱"
	IF めくり数 != 0
		サーチ範囲 = めくり数
	ELSE
		サーチ範囲 = 紙束最大値
	ENDIF
	実行者 = %듀얼리스트%
	参照先 = 덱
	上級値 = 300
ELSEIF 元参照先 == "EX덱"
	IF めくり数 != 0
		サーチ範囲 = めくり数
	ELSE
		サーチ範囲 = 紙束最大値
	ENDIF
	実行者 = %듀얼리스트%
	参照先 = EX덱
	上級値 = 400
ELSEIF 元参照先 == "제외"
	サーチ範囲 = 紙束最大値
	実行者 = %듀얼리스트%
	参照先 = 제외
	上級値 = 500

ELSEIF 元参照先 == "상대필드"
	サーチ範囲 = 5
	実行者 = %対面者%
	参照先 = 필드
	上級値 = 2000
ELSEIF 元参照先 == "상대마함"
	サーチ範囲 = 5
	実行者 = %対面者%
	参照先 = 마함
	上級値 = 2010
ELSEIF 元参照先 == "상대손패"
	サーチ範囲 = 紙束最大値
	実行者 = %対面者%
	参照先 = 손패
	上級値 = 2100
ELSEIF 元参照先 == "상대묘지"
	サーチ範囲 = 紙束最大値
	実行者 = %対面者%
	参照先 = 묘지
	上級値 = 2200
ELSEIF 元参照先 == "상대덱"
	IF めくり数 != 0
		サーチ範囲 = めくり数
	ELSE
		サーチ範囲 = 紙束最大値
	ENDIF
	実行者 = %対面者%
	参照先 = 덱
	上級値 = 2300
ELSEIF 元参照先 == "상대EX덱"

	IF めくり数 != 0
		サーチ範囲 = めくり数
	ELSE
		サーチ範囲 = 紙束最大値
	ENDIF
	実行者 = %対面者%
	参照先 = EX덱
	上級値 = 2400
ELSEIF 元参照先 == "상대제외"
	サーチ範囲 = 紙束最大値
	実行者 = %対面者%
	参照先 = 제외
	上級値 = 2500
ELSE
	サーチ範囲 = 紙束最大値
ENDIF

SIF 上級플래그 == 0
	上級値 = 0

FOR カウンタ,0,サーチ範囲
	SIF GETVAR(@"%実行者%_%参照先%:カウンタ") <= -1
		CONTINUE
		대상カードID = GETVAR(@"%実行者%_%参照先%:カウンタ")

	IF 효과条件固定 > -1
		SIF 효과条件固定 != カウンタ
			CONTINUE
	ENDIF
	IF 효과条件複数 > 0
		SIF MATCH(효과条件選択済, (カウンタ + 上級値))
			CONTINUE
	ENDIF
	IF 효과条件他所 > 0
		IF 元参照先 == ゾーン
			SIF カウンタ == 場所
				CONTINUE
		ENDIF
	ENDIF
	IF 듀얼리스트 != "編集"
		IF GETVAR(@"%듀얼리스트%_アザミナ呪") >= 1 && ゾーン != "어드밴스"
			IF 元参照先 == "묘지" || 元参照先 == "제외" || 元参照先 == "필드"
				CONTINUE
			ENDIF
		ENDIF
	ENDIF
	VARSET 条件달성
	CALL CARD_S_FILTER_SET(実行者,参照先,カウンタ,듀얼리스트,チェック플래그,カードID)
	SIF RESULT == 1
		条件달성 = 1
	CALL CARD_FILTER_SET(実行者,参照先,カウンタ,듀얼리스트,チェック플래그,カードID)
	SIF RESULT == 1
		条件달성 = 1
	SIF 효과条件상대モン && 元参照先 == "상대필드"
		条件달성 = 1
	SIF 条件달성 != 1
		CONTINUE

	IF ゾーン == "어드밴스"
		SIF GETVAR(@"%実行者%_MZ_不材:カウンタ") >= 1
			CONTINUE
	ENDIF
	IF 효과条件固名前起動 == 1
		TRYCALLFORM CARDNAME_{대상カードID}, "名前"
		효과条件固名前:カウンタD = %RESULTS%
		カウンタD ++
	ENDIF
	IF 듀얼리스트 == "자신"
		SIF チェック플래그 == 1
			SKIPDISP チェック플래그
		;상대カードの場合（上級値＋2000）
		IF (参照先 == "필드" || 参照先 == "마함") && 上級値 >= 2000
			IF 参照先 == "필드"		
				CALL CARD_SETCOLOR(実行者,参照先,カウンタ)
				;上級値を使い、裏側플래그まで確認している。
				TRYCALLFORM CARD_KANPRINT("상대필드",(カウンタ + 上級値),대상カードID)
			ELSEIF 参照先 == "마함"		
				CALL CARD_SETCOLOR(実行者,参照先,カウンタ)
				;上級値を使い、裏側플래그まで確認している。
				TRYCALLFORM CARD_KANPRINT("상대마함",(カウンタ + 上級値),대상カードID)
			ENDIF
			RESETCOLOR
		ELSE
			CALL CARD_SETCOLOR(実行者,参照先,カウンタ)
			;上級値を使っている。
			TRYCALLFORM CARD_KANPRINT(元参照先,(カウンタ + 上級値),대상カードID)
			RESETCOLOR
		ENDIF

	ELSEIF 듀얼리스트 == "상대"
		;ゾーン특수：上級소환の릴리스、モンスター묘지送りのためのやつ
		IF ゾーン == "어드밴스"
			IF 参照先 == "필드"
				IF GETVAR(@"%実行者%_MZ_攻:カウンタ") < 現在最小攻撃力
					現在最小攻撃力 = RESULT
					選択カード = カウンタ + 上級値
				ENDIF
			ELSE
				VARSET RESULT
				TRYCALLFORM CARD_{대상カードID}, "攻撃力"
				IF RESULT < 現在最小攻撃力
					現在最小攻撃力 = RESULT
					選択カード = カウンタ + 上級値
				ENDIF
			ENDIF
		ELSE
			IF 実行者 != 듀얼리스트
				IF 参照先 == "필드"
					IF GETVAR(@"%実行者%_MZ_攻:カウンタ") + 99999 > 現在最大攻撃力
						;Anonym修正
						現在最大攻撃力 = GETVAR(@"%実行者%_MZ_攻:カウンタ") + 99999
						選択カード = カウンタ + 上級値
					ENDIF
				ELSE
					VARSET RESULT
					TRYCALLFORM CARD_{대상カードID}, "攻撃力"
					IF  RESULT + 99999 > 現在最大攻撃力
						;Anonym修正
						現在最大攻撃力 = RESULT + 99999
						選択カード = カウンタ + 上級値
					ENDIF
				ENDIF
			ELSE
				IF 参照先 == "필드"
					IF GETVAR(@"%実行者%_MZ_攻:カウンタ") > 現在最大攻撃力
						選択カード = カウンタ + 上級値
					ENDIF
				ELSE
					VARSET RESULT
					TRYCALLFORM CARD_{대상カードID}, "攻撃力"
					攻撃力설정 = RESULT
					CALL ENEMY_PRIORITY_FILTER(대상カードID,攻撃力설정)	
					;PRINTFORML リザルト：{RESULT}/대상カードID:{대상カードID}
					IF RESULT > 現在最大攻撃力
						現在最大攻撃力 = RESULT
						選択カード = カウンタ + 上級値
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;合計数確認系
	条件合致数 ++
	IF 参照先 == "필드"
		条件合致レベル数 += GETVAR(@"%実行者%_MZ_星:カウンタ")
		条件合致攻撃力数 += GETVAR(@"%実行者%_MZ_攻:カウンタ")
		条件合致守備力数 += GETVAR(@"%実行者%_MZ_守:カウンタ")

		条件合致Ｘ素材数 += GETVAR(@"%実行者%_MZ_Ｘ:カウンタ") + 1
		条件合致魔力Ｃ数 += GETVAR(@"%実行者%_MZ_魔Ｃ:カウンタ")
		条件合致個別Ｃ数 += GETVAR(@"%実行者%_MZ_個Ｃ:カウンタ")
		条件合致悪性Ｃ数 += GETVAR(@"%実行者%_MZ_悪Ｃ:カウンタ")
		条件合致薬毒Ｃ数 += GETVAR(@"%実行者%_MZ_薬Ｃ:カウンタ")
	ELSE
		TRYCALLFORM CARD_{대상カードID}, "レベル"
		条件合致レベル数 += RESULT
		TRYCALLFORM CARD_{대상カードID}, "攻撃力"
		条件合致攻撃力数 += RESULT
		TRYCALLFORM CARD_{대상カードID}, "守備力"
		条件合致守備力数 += RESULT

		IF 参照先 == "마함"
			条件合致魔力Ｃ数 += GETVAR(@"%実行者%_魔_魔Ｃ:カウンタ")
			条件合致個別Ｃ数 += GETVAR(@"%実行者%_魔_個Ｃ:カウンタ")
			条件合致悪性Ｃ数 += GETVAR(@"%実行者%_魔_悪Ｃ:カウンタ")
			条件合致薬毒Ｃ数 += GETVAR(@"%実行者%_魔_薬Ｃ:カウンタ")
		ENDIF
	ENDIF
	SIF めくり数 == 99
		BREAK
NEXT


IF 上級플래그 && カウンタB <= 19
	カウンタB ++
	GOTO INPUTLOOP
ENDIF
$INPUTSKIP
VARSET RESULT
RESULT:1 = 条件合致数
RESULT:2 = 条件合致レベル数
RESULT:3 = 条件合致攻撃力数
RESULT:4 = 条件合致守備力数

RESULT:7 = 条件合致薬毒Ｃ数
RESULT:8 = 条件合致魔力Ｃ数
RESULT:9 = 条件合致個別Ｃ数
RESULT:10 = 条件合致悪性Ｃ数
RESULT:11 = 条件合致Ｘ素材数
SKIPDISP 0

IF 듀얼리스트 == "자신"
	SIF チェック플래그 != 1
		CUSTOMDRAWLINE —
	RETURN 0
ELSEIF 듀얼리스트 == "상대"
	IF 選択カード != -1
		RETURN 選択カード
	ELSE
		RETURN 9999
	ENDIF
ENDIF



;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――

@CARD_FILTER_SET(実行者,参照先,カウンタ,듀얼리스트,チェック플래그,カードID)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 実行者
#DIM DYNAMIC めくり数
#DIMS DYNAMIC 元参照先
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 설정カードID
#DIM DYNAMIC カードID
#DIMS DYNAMIC カード追カテ,10
#DIMS DYNAMIC カード改名

#DIMS DYNAMIC MZ魔판정
#DIM DYNAMIC チェック플래그
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 성별판정
#DIM DYNAMIC 캐릭터番号
;ザポルグ追記20250102
#DIM DYNAMIC 元レベル
#DIM DYNAMIC 現レベル

#DIM DYNAMIC 攻撃力설정
#DIM DYNAMIC 守備力설정
#DIM DYNAMIC 検出속성,99
#DIM DYNAMIC 検出종족,99

#DIM DYNAMIC 戻り値


;チェック플래그
;1： CARD_CHECK_BASIC用
;2： 특수소환可否の確認
;3： 除去판정用
;4： CARD_START_2用確認
;5： ERO효과用
;6： CARD_START用確認


SIF 参照先 == ""
	RETURN 0

IF チェック플래그 == 5
	戻り値 = -1
ELSE
	戻り値 = 0
ENDIF
SIF 듀얼리스트 == "" ;Anonym추가
	듀얼리스트 = %実行者%
;
IF チェック플래그 == 6
	参照先 = 묘지
	カウンタ = 0
	설정カードID = カードID
ELSEIF チェック플래그 == 4
	설정カードID = カードID
ELSE
	SIF GETVAR(@"%実行者%_%参照先%:カウンタ") == -1
		RETURN 戻り値
	SIF GETVAR(@"%実行者%_%参照先%:カウンタ") == 使用불가ゾーン
		RETURN 戻り値
	설정カードID = GETVAR(@"%実行者%_%参照先%:カウンタ")
ENDIF

IF 효과条件ID > -1
	VARSET 条件달성
	TRYCALLFORM CARD_{설정カードID}, "別途ID"
	IF RESULT >= 1
		SIF MATCH(효과条件ID, RESULT)
			条件달성 ++
	ELSE
		SIF MATCH(효과条件ID, 설정カードID)
			条件달성 ++
	ENDIF
	IF 参照先 == "필드"
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			SIF MATCH(효과条件ID, GETVAR(@"%実行者%_MZ_変化元ID:カウンタ"))
				条件달성 ++
		ENDIF
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_MZ_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_MZ_改名:カウンタ")%
			TRYCALLFORM CARDNAME_{효과条件ID}, "名前"
			SIF MATCH(RESULTS, カード改名)
				条件달성 ++
		ENDIF
	ELSEIF 参照先 == "마함"
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_魔_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_魔_改名:カウンタ")%
			TRYCALLFORM CARDNAME_{효과条件ID}, "名前"
			SIF MATCH(RESULTS, カード改名)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 == 0
		RETURN 戻り値
ENDIF



IF 효과条件固ID > -1
	VARSET 条件달성
	TRYCALLFORM CARD_{설정カードID}, "別途ID"
	IF RESULT >= 1
		SIF MATCH(효과条件固ID, RESULT)
			条件달성 ++
	ELSE
		SIF MATCH(효과条件固ID, 설정カードID)
			条件달성 ++
	ENDIF
	IF 参照先 == "필드"
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			SIF MATCH(효과条件固ID, GETVAR(@"%実行者%_MZ_変化元ID:カウンタ"))
				条件달성 ++
		ENDIF
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_MZ_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_MZ_改名:カウンタ")%
			TRYCALLFORM CARDNAME_{효과条件固ID}, "名前"
			SIF MATCH(RESULTS, カード改名)
				条件달성 ++
		ENDIF
	ELSEIF 参照先 == "마함"
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_魔_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_魔_改名:カウンタ")%
			TRYCALLFORM CARDNAME_{효과条件固ID}, "名前"
			SIF MATCH(RESULTS, カード改名)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 != 0
		RETURN 戻り値
ENDIF


;PRINTFORML ここ通過１
IF 효과条件名前 != ""
	VARSET 条件달성
	TRYCALLFORM CARD_{설정カードID}, "別途ID"
	IF RESULT >= 1
		TRYCALLFORM CARDNAME_{RESULT}, "名前"
	ELSE
		TRYCALLFORM CARDNAME_{설정カードID}, "名前"
	ENDIF
	SIF MATCH(효과条件名前, RESULTS)
		条件달성 ++

	IF 参照先 == "필드"
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_MZ_変化元ID:カウンタ")}, "名前"
			SIF MATCH(효과条件名前, RESULTS)
				条件달성 ++
		ENDIF
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_MZ_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_MZ_改名:カウンタ")%
			SIF MATCH(효과条件名前, カード改名)
				条件달성 ++
		ENDIF
	ELSEIF 参照先 == "마함"
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_魔_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_魔_改名:カウンタ")%
			SIF MATCH(효과条件名前, カード改名)
				条件달성 ++
		ENDIF
	ENDIF

	SIF 条件달성 == 0					
		RETURN 戻り値
ENDIF
IF 효과条件固名前 != ""
	VARSET 条件달성
	TRYCALLFORM CARDNAME_{설정カードID}, "名前"
	SIF MATCH(효과条件固名前, RESULTS)
		条件달성 ++
	IF 参照先 == "필드"
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_MZ_変化元ID:カウンタ")}, "名前"
			SIF MATCH(효과条件固名前, RESULTS)
				条件달성 ++
		ENDIF
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_MZ_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_MZ_改名:カウンタ")%
			SIF MATCH(효과条件固名前, カード改名)
				条件달성 ++
		ENDIF
	ELSEIF 参照先 == "마함"
		;改名されてると名前すべて失い、今の名前のみ
		IF GETVARS(@"%実行者%_魔_改名:カウンタ") != ""
			VARSET 条件달성
			カード改名 = %GETVARS(@"%実行者%_魔_改名:カウンタ")%
			SIF MATCH(효과条件固名前, カード改名)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 != 0
		RETURN 戻り値
ENDIF
;PRINTFORML ここ通過２　：효과条件カテゴリ：%효과条件カテゴリ%

IF 효과条件カテゴリ != ""
	VARSET 条件달성
	TRYCALLFORM CARDNAME_{설정カードID}, "カテゴリ"
	FOR カウンタB,0,5
		IF RESULTS:カウンタB != ""
			SIF MATCH(RESULTS:カウンタB, 효과条件カテゴリ)
				条件달성 ++
			SIF MATCH(효과条件カテゴリ, RESULTS:カウンタB)
				条件달성 ++
		ENDIF
	NEXT
	;PRINTFORML ここ通過２A　：효과条件カテゴリ：%효과条件カテゴリ%

	IF 参照先 == "필드" && 효과条件元々 == 0
		;改名されてるとカテゴリをすべて失い、추가カテゴリのみ
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_MZ_変化元ID:カウンタ")}, "カテゴリ"
			SIF MATCH(효과条件カテゴリ, RESULTS)
				条件달성 ++
			SIF MATCH(RESULTS, 효과条件カテゴリ)
				条件달성 ++
		ENDIF

		IF GETVARS(@"%実行者%_MZ_改名:カウンタ") != ""
			VARSET 条件달성
		ENDIF

		VARSET カード追カテ
		カード追カテ = %GETVARS(@"%実行者%_MZ_追カテ:カウンタ")%
		カード追カテ:1 = %GETVARS(@"%実行者%_MZ_追カテ2:カウンタ")%
		IF GETVARS(@"%実行者%_MZ_追カテ:カウンタ") != ""
			IF MATCH(효과条件カテゴリ, カード追カテ)
				条件달성 ++
			ENDIF
			IF MATCH(カード追カテ, 효과条件カテゴリ)
				条件달성 ++
			ENDIF
		ENDIF

	ELSEIF 参照先 == "마함"
		;改名されてるとカテゴリをすべて失い、추가カテゴリのみ
		IF GETVARS(@"%実行者%_魔_改名:カウンタ") != ""
			VARSET 条件달성
		ENDIF

		VARSET カード追カテ
		カード追カテ = %GETVARS(@"%実行者%_魔_追カテ:カウンタ")%
		カード追カテ:1 = %GETVARS(@"%実行者%_魔_追カテ2:カウンタ")%
		IF GETVARS(@"%実行者%_魔_追カテ:カウンタ") != ""
			SIF MATCH(효과条件カテゴリ, カード追カテ)
				条件달성 ++
			SIF MATCH(カード追カテ, 효과条件カテゴリ)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 == 0					
		RETURN 戻り値
ENDIF
;PRINTFORML ここ通過３
IF 효과条件固カテゴリ != ""
	VARSET 条件달성
	TRYCALLFORM CARDNAME_{설정カードID}, "カテゴリ"
	IF MATCH(RESULTS, 효과条件固カテゴリ)
		条件달성 ++
	ENDIF
	IF MATCH(효과条件固カテゴリ, RESULTS)
		条件달성 ++
	ENDIF
	IF 参照先 == "필드"
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_MZ_変化元ID:カウンタ")}, "カテゴリ"
			SIF MATCH(효과条件固カテゴリ, RESULTS)
				条件달성 ++
			SIF MATCH(RESULTS, 효과条件固カテゴリ)
				条件달성 ++
		ENDIF

		;改名されてるとカテゴリをすべて失い、추가カテゴリのみ
		IF GETVARS(@"%実行者%_MZ_改名:カウンタ") != ""
			VARSET 条件달성
		ENDIF
		
		VARSET カード追カテ
		カード追カテ = %GETVARS(@"%実行者%_MZ_追カテ:カウンタ")%
		カード追カテ:1 = %GETVARS(@"%実行者%_MZ_追カテ2:カウンタ")%
		IF GETVARS(@"%実行者%_MZ_追カテ:カウンタ") != ""
			IF MATCH(효과条件固カテゴリ, カード追カテ)
				条件달성 ++
			ENDIF
			IF MATCH(カード追カテ, 효과条件固カテゴリ)
				条件달성 ++
			ENDIF
		ENDIF
		
	ELSEIF 参照先 == "마함"
		;改名されてるとカテゴリをすべて失い、추가カテゴリのみ
		IF GETVARS(@"%実行者%_魔_改名:カウンタ") != ""
			VARSET 条件달성
		ENDIF

		VARSET カード追カテ
		カード追カテ = %GETVARS(@"%実行者%_魔_追カテ:カウンタ")%
		カード追カテ:1 = %GETVARS(@"%実行者%_魔_追カテ2:カウンタ")%
		IF GETVARS(@"%実行者%_魔_追カテ:カウンタ") != ""
			SIF MATCH(효과条件固カテゴリ, カード追カテ)
				条件달성 ++
			SIF MATCH(カード追カテ, 효과条件固カテゴリ)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 != 0					
		RETURN 戻り値
ENDIF

;PRINTFORML ここ通過４
IF 효과条件分割 > 0
	TRYCALLFORM CARD_{설정カードID}, "分割"
	IF 효과条件分割 == 分割在り
		SIF RESULT == 0
			RETURN 戻り値
	ELSEIF 효과条件分割 == 分割無し
		SIF RESULT != 0
			RETURN 戻り値
	ELSEIF 효과条件分割 == 펜듈럼以外
		SIF RESULT == 펜듈럼
			RETURN 戻り値
	ELSE
		SIF RESULT != 효과条件分割
			RETURN 戻り値
	ENDIF
ENDIF



IF 효과条件종류  > 0
	VARSET 条件달성
	TRYCALLFORM CARD_{설정カードID}, "종류"
	IF 효과条件종류 == モン全般
		SIF RESULT < モン全般
			条件달성 ++
	ELSEIF 효과条件종류 == トークン
		TRYCALLFORM CARD_{설정カードID}, "トークン"
		SIF RESULT == 1
			条件달성 ++
	ELSEIF 효과条件종류 == トークン以外
		TRYCALLFORM CARD_{설정カードID}, "トークン"
		SIF RESULT == 0
			条件달성 ++
	ELSEIF 효과条件종류  == EXモン全般
		SIF GROUPMATCH(RESULT, 융합, 싱크로, 엑시즈, 링크, 초차원)
			条件달성 ++
	ELSEIF 효과条件종류  == EX以外全般
		SIF !GROUPMATCH(RESULT, 융합, 싱크로, 엑시즈, 링크, 초차원) && RESULT < 10
			条件달성 ++
	ELSEIF 효과条件종류  == レベル持ち
		SIF !GROUPMATCH(RESULT, 엑시즈, 링크)
			条件달성 ++
	ELSEIF INRANGE(효과条件종류,의식以外,링크以外)
		SIF RESULT != 효과条件종류 - 10
			条件달성 ++
	ELSEIF 효과条件종류  == 효과全般
		SIF RESULT != 通常モン
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "分割"
		SIF RESULT == 通常モン
			VARSET 条件달성

	ELSEIF 효과条件종류  == 효과無し全般
		SIF RESULT == 通常モン
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "分割"
		SIF RESULT == 通常モン
			条件달성 ++

	ELSEIF 효과条件종류 == 마법全般
		SIF INRANGE(RESULT, 20, 29) 
			条件달성 ++
		IF チェック플래그 == 4 && 開始_효과種 == 손패起動
			TRYCALLFORM CARD_{설정カードID}, "分割"
			SIF RESULT == 펜듈럼
				条件달성 ++
		ENDIF
	ELSEIF 효과条件종류  == 함정全般
		SIF INRANGE(RESULT, 30, 39) 
			条件달성 ++
	ELSEIF 효과条件종류  == 마함全般
		SIF INRANGE(RESULT, 20, 39) 
			条件달성 ++	
	ELSEIF 효과条件종류 == 表置全般
		SIF !GROUPMATCH(RESULT, 通常마법, 装備마법, 速攻마법, 通常함정) 
			条件달성 ++	
	ENDIF

	SIF MATCH(효과条件종류, RESULT)
		条件달성 ++
	SIF MATCH(RESULT, 효과条件종류)
		条件달성 ++
	SIF 条件달성 == 0		
		RETURN 戻り値
ENDIF

IF GROUPMATCH(チェック플래그,2,7)
	TRYCALLFORM CARD_{설정カードID}, "종류"
	SIF RESULT > 10
		RETURN 戻り値
	IF 효과条件特不無視 == 0
		SIF RESULT == 3
			RETURN 戻り値
		TRYCALLFORM CARD_{설정カードID}, "특수불가"
		SIF RESULT == 1
			RETURN 戻り値
		IF チェック플래그 == 2
		CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先) ;Anonym修正
		SIF RESULT == 1
			RETURN 戻り値
	ELSE
		CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先) ;Anonym修正
		SIF RESULT == 1
			RETURN 戻り値
		ENDIF
	ELSE
		SIF モンスター数(듀얼리스트) >= 5
			RETURN 戻り値
		SIF GETVAR(@"%듀얼리스트%_SS阻害状態") >= 1
			RETURN 戻り値
	ENDIF
ENDIF






IF 参照先 == "필드" || 参照先 == "마함"
	IF 参照先 == "필드"
		MZ魔판정 = MZ
	ELSEIF 参照先 == "마함"
		MZ魔판정 = 魔
	ENDIF
	IF 효과条件표시 == -1 && チェック플래그 == 1
		CALLFORM CHECK_COND
		IF RESULT == 1
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 3
					RETURN 戻り値
			ELSEIF 参照先 == "마함"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 1
					RETURN 戻り値
			ENDIF
		ENDIF
	ENDIF

	IF 효과条件표시 >= 0
		;효과불가裏側표시
		SIF 효과条件표시 == 30 && GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 3
			RETURN 戻り値
		;裏側표시
		IF 효과条件표시 == 裏側표시
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 3
					RETURN 戻り値
			ELSEIF 参照先 == "마함"		
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 1
					RETURN 戻り値
			ENDIF
		ENDIF
		;表側표시
		IF 효과条件표시 == 表側표시
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 3
					RETURN 戻り値
			ELSEIF 参照先 == "마함"		
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 1
					RETURN 戻り値
			ENDIF
		ENDIF
		;その他
		IF 효과条件표시 == 守備全般
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 1
					RETURN 戻り値
			ENDIF
		ENDIF
		;효과条件표시 = 0はCHECKBASICをパスするため（モンスター全体をカウントしたい時とかに使用）
		SIF 효과条件표시 != 0 && 효과条件표시 <= 9 && GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 효과条件표시
			RETURN 戻り値
	ENDIF



	IF 효과条件個Ｃ所持 > 0
		IF 효과条件個Ｃ所持 == 888
			SIF GETVAR(@"%実行者%_%MZ魔판정%_個Ｃ:カウンタ") >= 1
				RETURN 戻り値
		ELSEIF 효과条件個Ｃ所持 == 999
			SIF GETVAR(@"%実行者%_%MZ魔판정%_個Ｃ:カウンタ") == 0
				RETURN 戻り値
		ELSE
			SIF GETVAR(@"%実行者%_%MZ魔판정%_個Ｃ:カウンタ") < 효과条件個Ｃ所持
				RETURN 戻り値
		ENDIF
	ENDIF
	IF 효과条件魔Ｃ所持 > 0
		IF 효과条件魔Ｃ所持 == 999
			SIF GETVAR(@"%実行者%_%MZ魔판정%_魔Ｃ:カウンタ") == 0
				RETURN 戻り値
		ELSE
			SIF GETVAR(@"%実行者%_%MZ魔판정%_魔Ｃ:カウンタ") < 효과条件魔Ｃ所持
				RETURN 戻り値
		ENDIF
	ENDIF
	IF 효과条件悪Ｃ所持 > 0
		IF 효과条件悪Ｃ所持 == 999
			SIF GETVAR(@"%実行者%_%MZ魔판정%_悪Ｃ:カウンタ") == 0
				RETURN 戻り値
		ELSE
			SIF GETVAR(@"%実行者%_%MZ魔판정%_悪Ｃ:カウンタ") < 효과条件魔Ｃ所持
				RETURN 戻り値
		ENDIF
	ENDIF
	IF 효과条件悪Ｃ所持 > 0
		IF 효과条件悪Ｃ所持 == 999
			SIF GETVAR(@"%実行者%_%MZ魔판정%_悪Ｃ:カウンタ") == 0
				RETURN 戻り値
		ELSEIF 효과条件悪Ｃ所持 == 777 && 実行者 != 듀얼리스트
			SIF GETVAR(@"%実行者%_%MZ魔판정%_悪Ｃ:カウンタ") < 1
				RETURN 戻り値
		ELSEIF 효과条件悪Ｃ所持 >= 1
			SIF GETVAR(@"%実行者%_%MZ魔판정%_悪Ｃ:カウンタ") < 효과条件悪Ｃ所持
				RETURN 戻り値
		ENDIF
	ENDIF
	IF 효과条件条件達生 > 0
		IF 효과条件条件達生 == 888
			SIF GETVAR(@"%実行者%_%MZ魔판정%_条達:カウンタ") >= 1
				RETURN 戻り値
		ELSEIF 효과条件条件達生 == 999
			SIF GETVAR(@"%実行者%_%MZ魔판정%_条達:カウンタ") == 0
				RETURN 戻り値
		ELSE
			SIF GETVAR(@"%実行者%_%MZ魔판정%_条達:カウンタ") < 효과条件条件達生
				RETURN 戻り値
		ENDIF
	ENDIF

	IF 효과条件装備先 >= 0 && 参照先 == "마함"
		SIF GETVAR(@"%実行者%_魔_装:カウンタ") != 場所
			RETURN 戻り値
	ENDIF

	IF チェック플래그 == 5
		SIF GETVAR(@"%実行者%_%MZ魔판정%_リ数:カウンタ") >= 10
			RETURN 戻り値
	ENDIF

	IF 효과条件除去種 != "" && チェック플래그 != 3
		IF 参照先 == "필드"
			CALL CARD_REMOVAL_ABLE(実行者,듀얼리스트,"単体",カウンタ,효과条件除去種,カードID)
			SIF RESULT != 1
				RETURN 戻り値
		ELSEIF 参照先 == "마함"
			CALL CARD_REMOVAL_ABLE(実行者,듀얼리스트,"単体",カウンタ,효과条件除去種,カードID,1)
			SIF RESULT != 1
				RETURN 戻り値
		ENDIF
	ENDIF
	
	IF 효과条件所有 >= 1
		IF 효과条件所有 == 1
			IF 듀얼리스트 == "자신"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 1
					RETURN 戻り値
			ELSEIF 듀얼리스트 == "상대"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 2
					RETURN 戻り値
			ENDIF
		ELSEIF 효과条件所有 == 2
			IF 듀얼리스트 == "자신"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 2
					RETURN 戻り値
			ELSEIF 듀얼리스트 == "상대"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 1
					RETURN 戻り値
			ENDIF
		ENDIF
	ENDIF
ENDIF


IF 参照先 == "필드"
	IF 효과条件重ねてＸ >= 1
		SIF GETVAR(@"%実行者%_MZ_不材:カウンタ") >= 1
			RETURN 戻り値
		SIF GETVAR(@"%実行者%_MZ_表:カウンタ") == 3 && 효과条件표시 != 裏側표시
			RETURN 戻り値
	ENDIF
	IF 효과条件攻撃可 >= 1
		SIF GETVAR(@"%実行者%_MZ_攻避:カウンタ") >= 1
			RETURN 戻り値
	ENDIF
	IF 효과条件Ｘ素所持 > 0
		IF 효과条件Ｘ素所持 == 999
			SIF GETVAR(@"%実行者%_MZ_Ｘ:カウンタ") <= -1
				RETURN 戻り値

		ELSEIF 효과条件Ｘ素所持 == 888
			SIF GETVAR(@"%実行者%_MZ_Ｘ:カウンタ") >= 0
				RETURN 戻り値

		ELSE
			SIF GETVAR(@"%実行者%_MZ_Ｘ:カウンタ") + 1 < 효과条件Ｘ素所持
				RETURN 戻り値
		ENDIF
	ENDIF


	IF 효과条件튜너 > 0
		SIF GETVAR(@"%実行者%_MZ_Ｔ:カウンタ") == 0
			RETURN 戻り値
	ENDIF
	IF 효과条件속성 > 0
		VARSET 条件달성
		SIF MATCH(효과条件속성, GETVAR(@"%実行者%_MZ_属:カウンタ"))
			条件달성 ++
		;複数속성チェック
		TRYCALLFORM CARD_{설정カードID}, "속성"
		IF RESULT:1 >= 1
			VARSET 検出속성, -1
			FOR カウンタB,0,10
				SIF RESULT:カウンタB <= 0
					CONTINUE
				検出속성:カウンタB = RESULT:カウンタB
			NEXT
			FOR カウンタB,1,10
				IF 検出속성:カウンタB != -1
					SIF MATCH(検出속성:カウンタB, 효과条件속성)
						条件달성 ++
					SIF MATCH(효과条件속성, 検出속성:カウンタB)
						条件달성 ++
				ENDIF
			NEXT
		ENDIF
		SIF 条件달성 == 0	
			RETURN 戻り値
	ENDIF
	IF 효과条件종족 > 0
		VARSET 条件달성
		SIF MATCH(효과条件종족, GETVAR(@"%実行者%_MZ_種:カウンタ"))
			条件달성 ++
		;複数종족チェック
		TRYCALLFORM CARD_{설정カードID}, "종족"
		IF RESULT:1 >= 1
			VARSET 検出종족, -1
			FOR カウンタB,0,10
				SIF RESULT:カウンタB <= 0
					CONTINUE
				検出종족:カウンタB = RESULT:カウンタB
			NEXT
			FOR カウンタB,1,10
				IF 検出종족:カウンタB != -1
					SIF MATCH(検出종족:カウンタB, 효과条件종족)
						条件달성 ++
					SIF MATCH(효과条件종족, 検出종족:カウンタB)
						条件달성 ++
				ENDIF
			NEXT
		ENDIF
		SIF 条件달성 == 0	
			RETURN 戻り値
	ENDIF

	IF 효과条件レベル > 0
		VARSET 条件달성
		SIF MATCH(효과条件レベル, GETVAR(@"%実行者%_MZ_星:カウンタ"))
			条件달성 ++
		SIF 条件달성 == 0		
			RETURN 戻り値
	ENDIF
	;ザポルグ追記20250102
	IF 효과条件変レベル > 0
		TRYCALLFORM CARD_{설정カードID}, "レベル"
		元レベル = RESULT
		現レベル = GETVAR(@"%듀얼리스트%_MZ_星:カウンタ")
		IF 효과条件変レベル == 1
			SIF 元レベル >= 現レベル
				RETURN 戻り値
		ELSEIF 효과条件変レベル == 2
			SIF 元レベル <= 現レベル
				RETURN 戻り値
		ELSEIF 효과条件変レベル == 3
			SIF 元レベル == 現レベル
				RETURN 戻り値
		ENDIF
	ENDIF
	;ここまで
	IF 효과条件攻撃力 > 0
		SIF GETVAR(@"%実行者%_MZ_攻:カウンタ") > 효과条件攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件固攻撃力 >= 0
		SIF GETVAR(@"%実行者%_MZ_攻:カウンタ") != 효과条件固攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件以攻撃力 > 0
		SIF GETVAR(@"%実行者%_MZ_攻:カウンタ") < 효과条件以攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件元固攻撃力 >= 0
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		SIF RESULT != 효과条件元固攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件守備力 > 0
		SIF GETVAR(@"%実行者%_MZ_守:カウンタ")  > 효과条件守備力
			RETURN 戻り値
	ENDIF
	IF 효과条件固守備力 >= 0
		SIF GETVAR(@"%実行者%_MZ_守:カウンタ")  != 효과条件固守備力
			RETURN 戻り値
	ENDIF
	IF 효과条件以守備力 > 0
		SIF GETVAR(@"%実行者%_MZ_守:カウンタ") < 효과条件以守備力
			RETURN 戻り値
	ENDIF
	IF 효과条件元固守備力 >= 0
		TRYCALLFORM CARD_{설정カードID}, "守備力"
		SIF RESULT != 효과条件元固守備力
			RETURN 戻り値
	ENDIF

	IF 효과条件同攻守 > 0
		攻撃力설정 = GETVAR(@"%実行者%_MZ_攻:カウンタ") 
		守備力설정 = GETVAR(@"%実行者%_MZ_守:カウンタ") 
		SIF 攻撃力설정 != 守備力설정
			RETURN 戻り値
	ENDIF

	IF 효과条件召種 > 0
		IF 효과条件召種 == 召種全특수
			SIF GETVAR(@"%実行者%_MZ_召種:カウンタ") == 召種通常
					RETURN 戻り値
		ELSE
			SIF GETVAR(@"%実行者%_MZ_召種:カウンタ") != 효과条件召種
				RETURN 戻り値
		ENDIF
	ENDIF

	IF 효과条件T1召種 > 0
		IF 효과条件T1召種 == 召種全특수
			SIF GETVAR(@"%実行者%_MZ_T1召種:カウンタ") == 召種通常
					RETURN 戻り値
		ELSEIF 효과条件T1召種 == 全召種
			SIF GETVAR(@"%実行者%_MZ_T1召種:カウンタ") == 0
					RETURN 戻り値
		ELSE
			SIF GETVAR(@"%実行者%_MZ_T1召種:カウンタ") != 효과条件召種
				RETURN 戻り値
		ENDIF
	ENDIF


	IF 효과条件성별 >= 0
		성별판정 = GETVAR(@"%実行者%_MZ_性:カウンタ")
		IF 효과条件성별 == 牝性
			SIF !GETBIT(성별판정,0)
				RETURN 戻り値
		ELSEIF 효과条件성별 == 雄性
			SIF !GETBIT(성별판정,1)
				RETURN 戻り値
		ELSEIF 효과条件성별 == 성별在
			SIF 성별판정 == 0
				RETURN 戻り値
		ELSEIF 효과条件성별 == 雌以外
			SIF 성별판정 == 1
				RETURN 戻り値
		ELSE
			SIF 성별판정 != 효과条件성별
				RETURN 戻り値
		ENDIF
	ENDIF
	IF 효과条件発情 != -1
		IF 효과条件発情 == 0 ;非発情
			SIF GETVAR(@"%実行者%_MZ_発情:カウンタ") != 0
				RETURN 戻り値
		ELSEIF 효과条件発情 >= 1 ;発情済
			SIF GETVAR(@"%実行者%_MZ_発情:カウンタ") < 1
				RETURN 戻り値
		ENDIF
	ENDIF
ELSE
	IF 효과条件튜너 > 0
		TRYCALLFORM CARD_{설정カードID}, "튜너"
		SIF RESULT == 0
			RETURN 戻り値
	ELSEIF 효과条件튜너 == -1
		TRYCALLFORM CARD_{설정カードID}, "튜너"
		SIF RESULT != 0
			RETURN 戻り値
	ENDIF


	IF 효과条件속성 > 0
		VARSET 条件달성
		;複数속성チェック
		TRYCALLFORM CARD_{설정カードID}, "속성"
		
		VARSET 検出속성, -1
		FOR カウンタB,0,10
			SIF RESULT:カウンタB <= 0
				CONTINUE
			検出속성:カウンタB = RESULT:カウンタB
		NEXT
		FOR カウンタB,0,10
			IF 検出속성:カウンタB != -1
				SIF MATCH(検出속성:カウンタB, 효과条件속성)
					条件달성 ++
				SIF MATCH(효과条件속성, 検出속성:カウンタB)
					条件달성 ++
			ENDIF
		NEXT
		SIF 条件달성 == 0	
			RETURN 戻り値
	ENDIF
	IF 효과条件종족 > 0
		VARSET 条件달성
		;複数속성チェック
		TRYCALLFORM CARD_{설정カードID}, "종족"
		
		VARSET 検出종족, -1
		FOR カウンタB,0,10
			SIF RESULT:カウンタB <= 0
				CONTINUE
			検出종족:カウンタB = RESULT:カウンタB
		NEXT
		FOR カウンタB,0,10
			IF 検出종족:カウンタB != -1
				SIF MATCH(検出종족:カウンタB, 효과条件종족)
					条件달성 ++
				SIF MATCH(효과条件종족, 検出종족:カウンタB)
					条件달성 ++
			ENDIF
		NEXT
		SIF 条件달성 == 0	
			RETURN 戻り値
	ENDIF



	IF 효과条件レベル > 0
		VARSET 条件달성
		TRYCALLFORM CARD_{설정カードID}, "レベル"
		SIF MATCH(효과条件レベル, RESULT)
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "コスト"
		SIF MATCH(효과条件レベル, RESULT)
			条件달성 ++
		SIF 条件달성 == 0		
			RETURN 戻り値
	ENDIF

	IF 효과条件攻撃力 > 0
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		SIF RESULT > 효과条件攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件固攻撃力 >= 0
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		SIF RESULT != 효과条件固攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件以攻撃力 > 0
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		SIF RESULT < 효과条件以攻撃力
			RETURN 戻り値
	ENDIF
	IF 효과条件元固攻撃力 >= 0
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		SIF RESULT != 효과条件元固攻撃力
			RETURN 戻り値
	ENDIF

	IF 효과条件守備力 > 0
		TRYCALLFORM CARD_{설정カードID}, "守備力"
		SIF RESULT > 효과条件守備力
			RETURN 戻り値
	ENDIF
	IF 효과条件固守備力 >= 0
		TRYCALLFORM CARD_{설정カードID}, "守備力"
		SIF RESULT != 효과条件固守備力
			RETURN 戻り値
	ENDIF
	IF 효과条件以守備力 > 0
		TRYCALLFORM CARD_{설정カードID}, "守備力"
		SIF RESULT < 효과条件以守備力
			RETURN 戻り値
	ENDIF
	IF 효과条件元固守備力 >= 0
		TRYCALLFORM CARD_{설정カードID}, "守備力"
		SIF RESULT != 효과条件元固守備力
			RETURN 戻り値
	ENDIF

	IF 효과条件同攻守 > 0
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		攻撃力설정 = RESULT
		TRYCALLFORM CARD_{설정カードID}, "守備力"
		守備力설정 = RESULT
		SIF 攻撃力설정 != 守備力설정
			RETURN 戻り値
	ENDIF

	IF 효과条件성별 >= 0
		;Anonym修正
		TRYCALLFORM CARD_{설정カードID}, "성별"
		성별판정 = RESULT
		캐릭터番号 = NO_TO_CHARANUM(설정カードID)
		IF 캐릭터番号 >= 1 && オプション_성별
			성별판정 = TALENT:캐릭터番号:성별
			IF 효과条件성별 == 牝性
				SIF !GETBIT(성별판정,0)
					RETURN 戻り値
			ELSEIF 효과条件성별 == 雄性
				SIF !GETBIT(성별판정,1)
					RETURN 戻り値
			ELSEIF 효과条件성별 == 성별在
				SIF 성별판정 == 0
					RETURN 戻り値
			ELSE
				SIF !MATCH(성별판정, 효과条件성별)
					RETURN 戻り値
			ENDIF
		ELSE
			IF 효과条件성별 == 牝性
				SIF !MATCH(성별판정,1)
					RETURN 戻り値
			ELSEIF 효과条件성별 == 雄性
				SIF !MATCH(성별판정,2)
					RETURN 戻り値
			ELSEIF 효과条件성별 == 성별在
				SIF 성별판정 == 0
					RETURN 戻り値
			ELSE
				SIF !MATCH(성별판정, 효과条件성별)
					RETURN 戻り値
			ENDIF
		ENDIF
	ENDIF

	IF 효과条件所有 >= 1
		IF 효과条件所有 == 1
			IF 듀얼리스트 == "자신"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 1
					RETURN 0
			ELSEIF 듀얼리스트 == "상대"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 2
					RETURN 0
			ENDIF
		ELSEIF 효과条件所有 == 2
			IF 듀얼리스트 == "자신"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 2
					RETURN 0
			ELSEIF 듀얼리스트 == "상대"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_所有:カウンタ") != 1
					RETURN 0
			ENDIF
		ENDIF
	ENDIF

	IF 효과条件소환可 > 0
		IF 효과条件特不無視 == 0
			TRYCALLFORM CARD_{설정カードID}, "종류"
			IF RESULT < 3 || RESULT == 초차원
				IF 효과条件소환可 == 1 || 효과条件소환可 == 3
					CALL CARD_SS_CHECK(実行者,설정カードID,参照先)
					SIF RESULT == 1
						RETURN 戻り値
				ELSEIF 효과条件소환可 == 2
					CALL CARD_SS_CHECK(実行者,설정カードID,参照先,1)
					SIF RESULT == 1
						RETURN 戻り値	
				ENDIF
			ELSEIF INRANGE(RESULT,3,10)

				IF RESULT == 의식
					IF 参照先 == "손패"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_손패:カウンタ")}(実行者,カウンタ + 100,1)
						SIF RESULT != 1
							RETURN 戻り値
					ELSEIF 参照先 == "묘지"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_묘지:カウンタ")}(実行者,カウンタ + 200,1)
						SIF RESULT != 1
							RETURN 戻り値
					ELSEIF 参照先 == "덱"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_덱:カウンタ")}(実行者,カウンタ + 300,1)
						SIF RESULT != 1
							RETURN 戻り値
					ELSEIF 参照先 == "제외"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_제외:カウンタ")}(実行者,カウンタ + 500,1)
						SIF RESULT != 1
							RETURN 戻り値
					ENDIF
				ELSEIF 参照先 == "EX덱"
					VARSET RESULT
					TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_EX덱:カウンタ")}(実行者,カウンタ,1)
					SIF RESULT != 1
						RETURN 戻り値
				ELSEIF 参照先 != "EX덱" ;Anonym추가
					IF 효과条件소환可 == 1 || 효과条件소환可 == 3
						CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先)
						SIF RESULT == 1
							RETURN 戻り値
					ELSEIF 효과条件소환可 == 2
						CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先,1)
						SIF RESULT == 1
						RETURN 戻り値
					ENDIF
				ENDIF
			ELSE
				IF 효과条件소환可 == 3
					CALL CARD_SS_CHECK(実行者,설정カードID,参照先)
					SIF RESULT == 1
						RETURN 戻り値
				ELSE
					RETURN 戻り値
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF 효과条件RUM >= 1
	TRYCALLFORM CARDRUM_{효과条件RUM}(実行者,参照先,カウンタ,설정カードID)
	SIF RESULT != 1
		RETURN 戻り値
ENDIF

;PRINTFORML チェック플래그:{チェック플래그}
IF チェック플래그 == 5
	CALL SET_FIELD_INFO(実行者,参照先,カウンタ,"リ数",1)
	CALL CARD_EROTICS_FIELD(実行者,参照先,カウンタ)
	RETURN RESULT
ENDIF

RETURN 1


;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――

@CARD_S_FILTER_SET(実行者,参照先,カウンタ,듀얼리스트,チェック플래그,カードID)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 実行者
#DIM DYNAMIC めくり数
#DIMS DYNAMIC 元参照先
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 설정カードID
#DIM DYNAMIC カードID
#DIMS DYNAMIC カード追カテ,10
#DIM DYNAMIC チェック플래그
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 検出속성,99
#DIM DYNAMIC 検出종족,99
#DIMS DYNAMIC MZ魔판정

SIF 参照先 == ""
	RETURN 0


IF チェック플래그 == 6
	参照先 = 묘지
	カウンタ = 0
	설정カードID = カードID
ELSE
	SIF GETVAR(@"%実行者%_%参照先%:カウンタ") == -1
		RETURN 0
	SIF GETVAR(@"%実行者%_%参照先%:カウンタ") == 使用불가ゾーン
		RETURN 0

	설정カードID = GETVAR(@"%実行者%_%参照先%:カウンタ")
ENDIF

IF 효과条件スーパー名前 != ""
	VARSET 条件달성
	TRYCALLFORM CARD_{설정カードID}, "別途ID"
	IF RESULT >= 1
		TRYCALLFORM CARDNAME_{RESULT}, "名前"
	ELSE
		TRYCALLFORM CARDNAME_{설정カードID}, "名前"
	ENDIF
	SIF MATCH(효과条件スーパー名前, RESULTS)
		条件달성 ++

	IF 参照先 == "필드"
		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_MZ_変化元ID:カウンタ")}, "名前"
			SIF MATCH(효과条件スーパー名前, RESULTS)
				条件달성 ++
		ENDIF
	ELSEIF 参照先 == "마함"
		IF GETVAR(@"%実行者%_魔_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_魔_変化元ID:カウンタ")}, "名前"
			SIF MATCH(효과条件スーパー名前, RESULTS)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 == 0
		RETURN 0
ENDIF

IF 효과条件スーパーカテゴリ != ""
	VARSET 条件달성
	TRYCALLFORM CARDNAME_{설정カードID}, "カテゴリ"
	FOR カウンタB,0,5
		IF RESULTS:カウンタB != ""
			SIF MATCH(RESULTS:カウンタB, 효과条件スーパーカテゴリ)
				条件달성 ++
			SIF MATCH(효과条件スーパーカテゴリ, RESULTS:カウンタB)
				条件달성 ++
		ENDIF
	NEXT
	IF 参照先 == "필드"
		VARSET カード追カテ
		カード追カテ = %GETVARS(@"%実行者%_MZ_追カテ:カウンタ")%
		カード追カテ:1 = %GETVARS(@"%実行者%_MZ_追カテ2:カウンタ")%
		IF GETVARS(@"%実行者%_MZ_追カテ:カウンタ") != ""
			IF MATCH(효과条件スーパーカテゴリ, カード追カテ)
				条件달성 ++
			ENDIF
			IF MATCH(カード追カテ, 효과条件スーパーカテゴリ)
				条件달성 ++
			ENDIF
		ENDIF

		IF GETVAR(@"%実行者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%実行者%_MZ_変化元ID:カウンタ")}, "カテゴリ"
			IF MATCH(RESULTS, 효과条件スーパーカテゴリ)
				条件달성 ++
			ENDIF
			IF MATCH(효과条件スーパーカテゴリ, RESULTS)
				条件달성 ++
			ENDIF
		ENDIF
	ENDIF
	IF 参照先 == "마함"
		VARSET カード追カテ
		カード追カテ = %GETVARS(@"%実行者%_魔_追カテ:カウンタ")%
		カード追カテ:1 = %GETVARS(@"%実行者%_魔_追カテ2:カウンタ")%
		IF GETVARS(@"%実行者%_魔_追カテ:カウンタ") != ""
			SIF MATCH(효과条件スーパーカテゴリ, カード追カテ)
				条件달성 ++
			SIF MATCH(カード追カテ, 효과条件スーパーカテゴリ)
				条件달성 ++
		ENDIF
	ENDIF
	SIF 条件달성 == 0
		RETURN 0
ENDIF

IF 효과条件スーパー종류 > 0
	VARSET 条件달성
	TRYCALLFORM CARD_{설정カードID}, "종류"
	IF 효과条件スーパー종류 == モン全般
		SIF RESULT < モン全般
			条件달성 ++
	ELSEIF 효과条件スーパー종류 == EXモン全般
		SIF GROUPMATCH(RESULT, 융합, 싱크로, 엑시즈, 링크, 초차원)
			条件달성 ++
	ELSEIF 효과条件スーパー종류 == EX以外全般
		SIF !GROUPMATCH(RESULT, 융합, 싱크로, 엑시즈, 링크, 초차원) && RESULT < 10
			条件달성 ++
	
	ELSEIF INRANGE(효과条件スーパー종류,의식以外,링크以外)
		SIF RESULT != 효과条件スーパー종류 - 10
			条件달성 ++

	ELSEIF 효과条件スーパー종류 == 효과全般
		SIF RESULT != 通常モン
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "分割"
		SIF RESULT != 通常モン
			条件달성 ++
	ELSEIF 효과条件スーパー종류 == 효과無し全般
		SIF RESULT == 通常モン
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "分割"
		SIF RESULT == 通常モン
			条件달성 ++

	ELSEIF 효과条件スーパー종류 == 마법全般
		SIF INRANGE(RESULT, 20, 29) 
			条件달성 ++
	ELSEIF 효과条件スーパー종류 == 함정全般
		SIF INRANGE(RESULT, 30, 39) 
			条件달성 ++
	ELSEIF 효과条件スーパー종류 == 마함全般
		SIF INRANGE(RESULT, 20, 39) 
			条件달성 ++	
	ELSE
		SIF MATCH(효과条件スーパー종류, RESULT)
			条件달성 ++
		SIF MATCH(RESULT, 효과条件スーパー종류)
			条件달성 ++
	ENDIF
	SIF 条件달성 == 0
		RETURN 0
ENDIF

IF 효과条件スーパー속성 > 0
	VARSET 条件달성
	IF 参照先 == "필드"
		SIF MATCH(효과条件スーパー속성, GETVAR(@"%実行者%_MZ_属:カウンタ"))
			条件달성 ++
		;複数속성チェック
		TRYCALLFORM CARD_{설정カードID}, "속성"
		IF RESULT:1 >= 1
			VARSET 検出속성, -1
			FOR カウンタB,0,10
				SIF RESULT:カウンタB <= 0
					CONTINUE
				検出속성:カウンタB = RESULT:カウンタB
			NEXT
			FOR カウンタB,1,10
				IF 検出속성:カウンタB != -1
					SIF MATCH(検出속성:カウンタB, 효과条件スーパー속성)
						条件달성 ++
					SIF MATCH(효과条件スーパー속성, 検出속성:カウンタB)
						条件달성 ++
				ENDIF
			NEXT
		ENDIF
	ELSE
		;複数속성チェック
		TRYCALLFORM CARD_{설정カードID}, "속성"
		VARSET 検出속성, -1
		FOR カウンタB,0,10
			SIF RESULT:カウンタB <= 0
				CONTINUE
			検出속성:カウンタB = RESULT:カウンタB
		NEXT
		FOR カウンタB,0,10
			IF 検出속성:カウンタB != -1
				SIF MATCH(検出속성:カウンタB, 효과条件スーパー속성)
					条件달성 ++
				SIF MATCH(효과条件スーパー속성, 検出속성:カウンタB)
					条件달성 ++
			ENDIF
		NEXT
	ENDIF
	SIF 条件달성 == 0
		RETURN 0
ENDIF

IF 효과条件スーパー종족 > 0
	VARSET 条件달성
	IF 参照先 == "필드"
		SIF MATCH(효과条件スーパー종족, GETVAR(@"%実行者%_MZ_種:カウンタ"))
			条件달성 ++
		;複数종족チェック
		TRYCALLFORM CARD_{설정カードID}, "종족"
		IF RESULT:1 >= 1
			VARSET 検出종족, -1
			FOR カウンタB,0,10
				SIF RESULT:カウンタB <= 0
					CONTINUE
				検出종족:カウンタB = RESULT:カウンタB
			NEXT
			FOR カウンタB,1,10
				IF 検出종족:カウンタB != -1
					SIF MATCH(検出종족:カウンタB, 효과条件スーパー종족)
						条件달성 ++
					SIF MATCH(효과条件スーパー종족, 検出종족:カウンタB)
						条件달성 ++
				ENDIF
			NEXT
		ENDIF
		SIF 条件달성 == 0	
			RETURN 0
	ELSE
		;複数종족チェック
		TRYCALLFORM CARD_{설정カードID}, "종족"
		VARSET 検出종족, -1
		FOR カウンタB,0,10
			SIF RESULT:カウンタB <= 0
				CONTINUE
			検出종족:カウンタB = RESULT:カウンタB
		NEXT
		FOR カウンタB,0,10
			IF 検出종족:カウンタB != -1
				SIF MATCH(検出종족:カウンタB, 효과条件スーパー종족)
					条件달성 ++
				SIF MATCH(효과条件スーパー종족, 検出종족:カウンタB)
					条件달성 ++
			ENDIF
		NEXT
	ENDIF
	SIF 条件달성 == 0	
		RETURN 0
ENDIF

IF 효과条件スーパーレベル > 0
	VARSET 条件달성
	IF 参照先 == "필드"
		SIF MATCH(효과条件スーパーレベル, GETVAR(@"%実行者%_MZ_星:カウンタ"))
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "コスト"
		SIF MATCH(효과条件スーパーレベル, RESULT)
			条件달성 ++
	ELSE
		TRYCALLFORM CARD_{설정カードID}, "レベル"
		SIF MATCH(효과条件スーパーレベル, RESULT)
			条件달성 ++
		TRYCALLFORM CARD_{설정カードID}, "コスト"
		SIF MATCH(효과条件スーパーレベル, RESULT)
			条件달성 ++
	ENDIF
	SIF 条件달성 == 0		
		RETURN 0
ENDIF

VARSET RESULT
IF 효과条件スーパー튜너 > 0
	TRYCALLFORM CARD_{설정カードID}, "튜너"
	SIF RESULT == 0
		RETURN 0
ELSEIF 효과条件スーパー튜너 == -1
	TRYCALLFORM CARD_{설정カードID}, "튜너"
	SIF RESULT != 0
		RETURN 0
ENDIF

;Anonym추가
IF 参照先 == "필드" || 参照先 == "마함"
	IF 参照先 == "필드"
		MZ魔판정 = MZ
	ELSEIF 参照先 == "마함"
		MZ魔판정 = 魔
	ENDIF
	IF 효과条件표시 == -1 && チェック플래그 == 1
		CALLFORM CHECK_COND
		IF RESULT == 1
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 3
					RETURN 0
			ELSEIF 参照先 == "마함"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 1
					RETURN 0
			ENDIF
		ENDIF
	ENDIF

	IF 효과条件표시 >= 0
		;효과불가裏側표시
		SIF 효과条件표시 == 30 && GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 3
			RETURN 0
		;裏側표시
		IF 효과条件표시 == 裏側표시
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 3
					RETURN 0
			ELSEIF 参照先 == "마함"		
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 1
					RETURN 0
			ENDIF
		ENDIF
		;表側표시
		IF 효과条件표시 == 表側표시
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 3
					RETURN 0
			ELSEIF 参照先 == "마함"		
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 1
					RETURN 0
			ENDIF
		ENDIF
		;その他
		IF 효과条件표시 == 守備全般
			IF 参照先 == "필드"
				SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 1
					RETURN 0
			ENDIF
		ENDIF
		;효과条件표시 = 0はCHECKBASICをパスするため（モンスター全体をカウントしたい時とかに使用）
		SIF 효과条件표시 != 0 && 효과条件표시 <= 9 && GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 효과条件표시
			RETURN 0
	ENDIF
	
ENDIF




;ザポルグ追記20240929
IF 효과条件スーパー攻撃力 > 0
	VARSET RESULT
	TRYCALLFORM CARD_{설정カードID}, "종류"
	IF RESULT < モン全般
		VARSET RESULT
		TRYCALLFORM CARD_{설정カードID}, "攻撃力"
		SIF RESULT > 효과条件攻撃力
			RETURN 0
	ENDIF
ENDIF


IF チェック플래그 == 2
	VARSET RESULT
	IF 효과条件特不無視 != 2
		TRYCALLFORM CARD_{설정カードID}, "종류"
		SIF RESULT > 10
			RETURN 0
		IF 효과条件特不無視 == 0
			SIF RESULT == 3
				RETURN 0
			TRYCALLFORM CARD_{설정カードID}, "특수불가"
			SIF RESULT == 1
				RETURN 0
			CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先)
			SIF RESULT == 1
				RETURN 0
			ELSE
			SIF モンスター数(듀얼리스트) >= 5
				RETURN 0
			SIF GETVAR(@"%듀얼리스트%_SS阻害状態") >= 1
				RETURN 0
		ENDIF
	ENDIF
ENDIF

CALL S_FILTER_EXIST
SIF RESULT == 0
	RETURN 0

IF 参照先 != "필드"
	IF 효과条件소환可 > 0
		IF 효과条件特不無視 == 0
			TRYCALLFORM CARD_{설정カードID}, "종류"
			IF RESULT < 3 || RESULT == 초차원
				IF 효과条件소환可 == 1 || 효과条件소환可 == 3
					CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先)
					SIF RESULT == 1
						RETURN 0
				ELSEIF 효과条件소환可 == 2
					CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先,1)
					SIF RESULT == 1
						RETURN 0
				ENDIF
			ELSEIF INRANGE(RESULT,3,10)
				IF RESULT == 의식
					IF 参照先 == "손패"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_손패:カウンタ")}(実行者,カウンタ + 100,1)
						SIF RESULT != 1
							RETURN 0
					ELSEIF 参照先 == "묘지"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_묘지:カウンタ")}(実行者,カウンタ + 200,1)
						SIF RESULT != 1
							RETURN 0
					ELSEIF 参照先 == "덱"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_덱:カウンタ")}(実行者,カウンタ + 300,1)
						SIF RESULT != 1
							RETURN 0
					ELSEIF 参照先 == "제외"
						TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_제외:カウンタ")}(実行者,カウンタ + 500,1)
						SIF RESULT != 1
							RETURN 0
					ENDIF
				;EX덱を参照に確認している
				ELSEIF 参照先 == "EX덱"
					VARSET RESULT
					TRYCALLFORM CARDSUMMON_{GETVAR(@"%実行者%_EX덱:カウンタ")}(実行者,カウンタ,1)
					SIF RESULT != 1
						RETURN 0
				;EX덱以外を参照に確認
				ELSEIF 参照先 != "EX덱" ;Anonym추가
					IF 효과条件소환可 == 1 || 효과条件소환可 == 3
						CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先)
						SIF RESULT == 1
							RETURN 0
					ELSEIF 효과条件소환可 == 2
						CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先,1)
						SIF RESULT == 1
							RETURN 0
					ENDIF
				ENDIF
			ELSE
				IF 효과条件소환可 == 3
					CALL CARD_SS_CHECK(듀얼리스트,설정カードID,参照先)
					SIF RESULT == 1
						RETURN 0
				ELSE
					RETURN 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF



RETURN 1


@S_FILTER_EXIST
VARSET RESULT
SIF 효과条件スーパー名前 != ""
	RETURN 1
SIF 효과条件スーパーカテゴリ != ""
	RETURN 1
SIF 효과条件スーパー종류 > 0
	RETURN 1
SIF 효과条件スーパー속성 > 0
	RETURN 1
SIF 효과条件スーパー종족 > 0
	RETURN 1
SIF 효과条件スーパーレベル > 0
	RETURN 1
SIF 효과条件スーパー튜너 > 0
	RETURN 1
;ザポルグ追記20240929
SIF 효과条件スーパー攻撃力 > 0
	RETURN 1
RETURN 0

;待機された효과를 待機する場所。
@SELECT_UI_SET(実行者,選択カード_ID,설정타입,選択カード,종류,ゾーン)
#DIMS DYNAMIC 実行者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 설정타입
#DIM DYNAMIC 選択カード

CALL CARD_UI_RESET
IF 実行者 == "상대"
	SETCOLOR C_W_RED
	CUSTOMDRAWLINE —
ELSE
	SETCOLOR C_W_BLUE
	CUSTOMDRAWLINE —
ENDIF
SELECTCASE 설정타입
CASE 1
	PRINT 【서치】
	確認듀얼리스트 = %実行者%
CASE 2
	PRINT 【묘지보내기】
	確認듀얼리스트 = %実行者%
CASE 3
	PRINT 【특수소환】
	確認듀얼리스트 = %実行者%
CASE 4
	PRINT 【선택】
	確認듀얼리스트 = %実行者%
CASE 5
	PRINT 【세트】
	確認듀얼리스트 = %実行者%
CASE 6
	PRINT 【장착처】
	確認듀얼리스트 = %実行者%
CASE 7
	PRINT 【장착선택】
	確認듀얼리스트 = %実行者%
CASE 8
	PRINT 【대기효과】
	確認듀얼리스트 =  %待機_듀얼리스트:選択カード%
	確認종류 = 待機_효과種:選択カード
	確認ゾーン = %待機_ゾーン:選択カード%
	確認場所 = 待機_場所:選択カード
CASE 9
	PRINT 【프리유발】
	確認듀얼리스트 = %実行者%
	確認종류 = フリー誘発
	確認ゾーン = %参照先（CS）%
	確認場所 = 選択カード
CASE 10
	PRINT 【효과 후 유발】
	確認듀얼리스트 = %実行者%
	確認종류 = 효과後誘発
	確認ゾーン = %参照先（CS）%
	確認場所 = 選択カード

CASE 15
	PRINT 【발동유발】
	確認듀얼리스트 = %実行者%
	確認종류 = 발동誘発
	確認ゾーン = %参照先（CS）%
	確認場所 = 選択カード


CASE 11
	PRINT 【제외】
	CALL CARD_SELECT(実行者,(選択カード + 2000))
	確認듀얼리스트 = %대상者（CS）%
	確認ゾーン = %参照先（CS）%
	確認場所 = 選択カード（CS）
CASE 12
	;両者確認
	PRINT 【제외】
	CALL CARD_SELECT(実行者,選択カード)
	確認듀얼리스트 = %대상者（CS）%
	確認ゾーン = %参照先（CS）%
	確認場所 = 選択カード（CS）
CASE 99
	PRINT 【발동확인】
	確認듀얼리스트 = %実行者%
	確認종류 = 종류
	確認ゾーン = %ゾーン%
	確認場所 = 選択カード
ENDSELECT
IF 実行者 == "상대"
	PRINTL 【상대가 고른 카드를 확인해주세요】
ELSE
	PRINTL 【선택 카드 확인】
ENDIF
CUSTOMDRAWLINE —
RESETCOLOR

IF GROUPMATCH(설정타입,1,2,3,4,5,6,7,11,12) && 実行者 == "자신"
	IF 確認ゾーン == "필드"
		IF GETVAR(@"%確認듀얼리스트%_MZ_表:確認場所") == 3 && 実行者 != 確認듀얼리스트
			TRYCALLFORM CARD_UI(999990,2)
		ELSE
			TRYCALLFORM CARD_UI_FIELD(確認듀얼리스트,確認場所,1)
		ENDIF
	ELSEIF 確認ゾーン == "마함"
		IF GETVAR(@"%確認듀얼리스트%_魔_表:確認場所") >= 2 && 実行者 != 確認듀얼리스트
			TRYCALLFORM CARD_UI(999990,2)
		ELSE
			TRYCALLFORM CARD_UI_MAGIC(確認듀얼리스트,確認場所,1)
		ENDIF
	ELSE
		TRYCALLFORM CARD_UI(選択カード_ID,2)
	ENDIF
ELSE
	TRYCALLFORM CARD_UI(選択カード_ID,2)
ENDIF
IF 実行者 == "자신"
	PRINTL
	CUSTOMDRAWLINE —
	PRINTL 이 카드로 괜찮겠습니까？
	PRINTL [0] - 예
	PRINTL [1] - 아니오
	BINPUT
	IF RESULT == 1
		RETURN 1
	ENDIF
ENDIF
RETURN 0

