@CARDNAME_72888,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 기사왕강림
	CASE "略称"
		RESULTS = 기사왕강림
	CASE "カテゴリ"
		RESULTS:0 = 로열 팔라딘
		RESULTS:1 = 알프레드
		RESULTS:2 = 의식 마법
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_72888(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 通常마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72888(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ②의 효과는 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(0,마함起動 ,손패起動)
PRINTL ①：레벨의 합계가 의식 소환한 몬스터의 레벨 이상이 되도록、
PRINTL 자신의 패 / 필드의 몬스터를 릴리스、
PRINTL 또는 릴리스 대신에 자신의 묘지에서 "로열 팔라딘" 몬스터를 제외하고、
PRINTL 패 / 덱에서 전사족 의식 몬스터 １장을 의식 소환한다。
CALL CARD_ABLE(1,身代효과)
PRINTL ②：자신 필드의 빛속성 몬스터가 전투 / 효과로 파괴될 경우、
PRINTL 대신에 묘지의 이 카드를 제외할 수 있다。


@CARDCAN_72888(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성,5

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72888)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲손패 = 1
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件종류 = 의식
	효과条件종족 = 전사족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条件달성 = 1

	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件종류 = 의식
	효과条件종족 = 전사족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条件달성:1 ++
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 로열 팔라딘
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条件달성:1 ++
	SIF 条件달성:1 == 2
		条件달성 = 1

	SIF 条件달성 == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF 발동済_2(듀얼리스트,72888)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	TRYCALLFORM CARD_{破壊대상ID}("속성")
	SIF RESULT != 빛속성
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_72888(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC EX参照
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択済カード
#DIM DYNAMIC レベル合計



IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72888)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件종류 = 의식
		효과条件종족 = 전사족
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		IF 選択カード != 9999
			CALL CARD_SELECT(듀얼리스트,RESULT)
			選択カード = RESULT
	
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 로열 팔라딘
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				PRINTL 묘지의 특정 카드를 제외하시겠습니까？
				CALL YN_CHOICE(듀얼리스트)
			ELSE
				RESULT = 1
			ENDIF
			IF RESULT == 0
				CALL RESETAREA_MAT
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_%参照先（CS）%:選択カード")}(듀얼리스트,選択カード)
				素材範囲묘지 = 1
				효과条件カテゴリ = 로열 팔라딘
				효과条件종류 = モン全般
				TRYCALLFORM RITSUMMON(듀얼리스트, 選択カード,参照先（CS）)
			ELSE
				CALL RESETAREA_MAT
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_%参照先（CS）%:選択カード")}(듀얼리스트,選択カード)
				素材範囲필드 = 1
				素材範囲손패 = 1
				TRYCALLFORM RITSUMMON(듀얼리스트, 選択カード,参照先（CS）)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72888,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		RETURN 1
	ENDIF
	RETURN 0
ENDIF


