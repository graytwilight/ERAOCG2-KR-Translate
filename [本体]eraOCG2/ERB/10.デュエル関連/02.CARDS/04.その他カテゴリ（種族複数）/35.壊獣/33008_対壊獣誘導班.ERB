@CARDNAME_33008,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 대파괴수유도반
	CASE "略称"
		RESULTS = 유도반
	CASE "カテゴリ"
		RESULTS:0 = 대파괴수
		RESULTS:1 = 게임 오리지널
ENDSELECT

@CARD_33008(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 사이킥족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 600
	CASE "守備力"
		RETURN 600
	CASE "성별"
		RETURN 雄性
	CASE "レア度"
		RETURN CMN
	CASE "生成カード"
		RETURN 133008
	CASE "추가日"
		RETURN 250923
ENDSELECT

RETURN 0

@CARD_EXPLANATION_33008(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①：패의 "파괴수" 또는 "대파괴수" 카드를 상대에게 보여주고 발동할 수 있다。
PRINTL 이 카드를 릴리스 하고、"파괴수" 몬스터 １장을 덱에서 패에 넣는다。
PRINTL 그 후、상대 필드에 "파괴수유도 비콘" １장을 수비 표시로 특수 소환한다。
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：필드에 "파괴수" 몬스터가 존재하는 경우、묘지의 이 카드를 제외하고 발동할 수 있다。
PRINTL 덱에서 "대파괴수" 마법 / 함정 카드 １장을 손패에 넣는다。

@CARDCAN_33008(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,33008)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,33008)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ:0 = 파괴수
	효과条件カテゴリ:1 = 대파괴수
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 파괴수
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,33008)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	효과条件カテゴリ = 파괴수
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 대파괴수
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ENDIF

@CARDEFFECT_33008(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,33008,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			PRINTL 보여줄 패를 선택해주십시오
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件カテゴリ:0 = 파괴수
			효과条件カテゴリ:1 = 대파괴수
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT != 9999
				PRINTL 패패에 넣을 카드를 선택해주십시오
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = 파괴수
				효과条件종류 = モン全般
				CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
				IF RESULT == 1
					CALL CARD_SS_CHECK(対面者,133008,"")
					IF RESULT != 1
						CALL FULLRESET
						CALL CARD_GENERATE(対面者,133008,"守備표시")
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,33008,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 대파괴수
		효과条件종류 = 마함全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ENDIF


@CARDNAME_133008,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 파괴수유도 비콘
	CASE "略称"
		RESULTS = 유도 비콘
	CASE "カテゴリ"
		RESULTS:0 = 대파괴수
ENDSELECT


@CARD_133008(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
ENDSELECT


@CARDCAN_133008(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

RETURN 0