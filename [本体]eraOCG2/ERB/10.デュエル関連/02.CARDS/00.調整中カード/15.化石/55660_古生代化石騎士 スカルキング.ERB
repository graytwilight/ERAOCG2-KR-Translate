@CARDNAME_55660,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 고생대 화석 기사 스컬킹
	CASE "略称"
		RESULTS = 고생대 화석 기사
	CASE "カテゴリ"
		RESULTS:0 = 화석
ENDSELECT


@CARD_55660(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 1300
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250919
ENDSELECT

RETURN 0

@CARD_EXPLANATION_55660(종류)
#DIM DYNAMIC 종류

PRINTL 원소정령족 몬스터＋레벨７ 이상의 몬스터
PRINTL 이 카드명의 ③의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,지속효과)
PRINTL ①：이 카드는 １번 배틀 페이즈중에 ２회 공격할 수 있다。
CALLFORM CARD_ABLE(2,지속효과)
PRINTL ②：이 카드가 수비 표시 몬스터를 공격한 경우、
PRINTL 그 수비력을 공격력이 넘은 만큼 전투 데미지를 준다。
CALLFORM CARD_ABLE(3,필드起動,フリー誘発)
PRINTL ③：상대 턴에、상대 묘지의 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 패를 １장 버리고、대상의 몬스터를 자신 필드에 특수 소환한다。

@CARDSUMMON_55660(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要素材数 = 2

IF EX参照 == 0 || EX参照 == 1
	素材条件종족 = 원소정령족
ELSEIF EX参照 >= 10
	素材条件レベル:0 = 7
	素材条件レベル:1 = 8
	素材条件レベル:2 = 9
	素材条件レベル:3 = 10
	素材条件レベル:4 = 11
	素材条件レベル:5 = 12
ENDIF

IF EX参照 == 1
	CALL FUSCAN(듀얼리스트,소환カード,55660) 
	RETURN RESULT
ENDIF

@CARDCAN_55660(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,55660)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_3(듀얼리스트,55660)
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	; 손패確認
	CALL FULLRESET
	효과範囲손패 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	; 상대묘지確認
	CALL FULLRESET
	효과範囲상대묘지 = 1
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ENDIF


@CARDEFFECT_55660(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"追戦",1)
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"貫通")
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,55660,3)
	IF RESULT == 0
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲상대묘지 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF

